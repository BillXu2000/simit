element Point
  b  : float;
  c  : float;
  id : int;
end

element Spring
  a : float;
end

extern points  : set{Point};
extern springs : set{Spring}(points,points);

func f(s : Spring, p : (Point*2)) -> (A : tensor[points,points](float))
  A(p(0),p(0)) =  s.a;
  A(p(0),p(1)) = -s.a;
  A(p(1),p(0)) = -s.a;
  A(p(1),p(1)) =  s.a;
end

func eye(p : Point) -> (I : tensor[points,points](float))
  I(p,p) = 1.0;
end

proc main
  b = points.b;
  A = map   f to springs reduce +;
  I = map eye to points reduce +;

  pointo1A = 0.01 * A;
  A = I - pointo1A;

  var xguess : tensor[points](float) = 0.0;   
  var x : tensor[points](float);

% start cg
  tol = 1e-6;
  maxiters = 50;
  xguess_A = xguess * A;
  r = b - (xguess_A);
  p = r;
  iter = 0;
  x = xguess;
  
  normr = norm(r);
  while (normr > tol) and (iter < maxiters)
    alpha = dot(r, r);
    Atimesp = A * p;
    denom = dot(p, Atimesp);
    alpha = alpha / denom;
    alphaxp = alpha * p;
    x = x + alphaxp;
    oldrsqn = dot(r,r);
    alphaxA = alpha * A;
    alphaxAxp = alphaxA * p;
    r = r - alphaxAxp;
    newrsqn = dot(r,r);
    beta = newrsqn/oldrsqn;
    betaxp = beta*p;
    p = r + betaxp;
    iter = iter + 1;
    normr = norm(r);
  end
% end cg
  points.c = x;
end
