%require "3.0"
%language "C++"

%defines
%define api.namespace { simit::internal }
%define parser_class_name { Parser }
%define parse.error verbose
%locations

%code requires {
  namespace simit {
  namespace internal {
    class Scanner;
    class ProgramContext;
  }}

  #include <vector>

  #include "util.h"
  #include "ir.h"
  #include "errors.h"
  #include "types.h"
}

%parse-param { Scanner *scanner };
%parse-param { ProgramContext *ctx };
%parse-param { std::vector<Error> *errors };

%code {
  #include <stdlib.h>
  #include <cassert>
  #include <iostream>
  #include <map>
  #include <set>
  #include <algorithm>

  #include "program_context.h"
  #include "scanner.h"
  #include "ir_codegen.h"
  using namespace std;
  using namespace simit::internal;
  using namespace simit::ir;

  std::string typeString(const Type &type) {
    std::stringstream ss;
    ss << type;
    std::string str = ss.str();
    if (type.isTensor() && type.toTensor()->isColumnVector) {
      str += "'";
    }
    return str;
  }

  #define REPORT_ERROR(msg, loc)  \
    do {                          \
      error((loc), (msg));        \
      YYERROR;                    \
    } while (0)

  #define REPORT_TYPE_MISSMATCH(t1, t2, loc)                          \
    do {                                                              \
      std::stringstream errorStr;                                     \
      errorStr << "type missmatch (" <<                               \
                  typeString(t1) << " and " << typeString(t2) << ")"; \
      REPORT_ERROR(errorStr.str(), loc);                              \
    } while (0)

  #define REPORT_INDEX_VARIABLE_MISSMATCH(numIndexVars, order, loc) \
    do {                                                            \
      REPORT_ERROR("wrong number of index variables (" +            \
                    to_string(numIndexVars) +                       \
                    " index variables, but tensor order is " +      \
                    to_string(order), loc);                         \
      } while (0)

  void Parser::error(const Parser::location_type &loc, const std::string &msg) {
    errors->push_back(Error(loc.begin.line, loc.begin.column,
                            loc.end.line, loc.end.column, msg));
  }

  #undef yylex
  #define yylex scanner->lex

  inline std::string convertAndFree(const char *str) {
    std::string result = std::string(str);
    free((void*)str);
    return result;
  }

  template <typename T>
  inline T convertAndDelete(T *obj) {
    auto result = T(*obj);
    delete obj;
    return result;
  }

  void transposeVector(const std::shared_ptr<Expression> &vec) {
    assert(vec->getType().isTensor());
    const TensorType *ttype = vec->getType().toTensor();
    assert(ttype->order() == 1);

    Type transposedVector = TensorType::make(ttype->componentType,
                                             ttype->dimensions,
                                             !ttype->isColumnVector);
    vec->setType(transposedVector);
  }

  bool compare(const Type &l, const Type &r, ProgramContext *ctx) {
    if (l.getKind() != r.getKind()) {
      return false;
    }

    if (l.isTensor()) {
      if (l.toTensor()->isColumnVector != r.toTensor()->isColumnVector) {
        return false;
      }
    }

    if (l != r) {
      return false;
    }
    return true;
  }

  #define CHECK_IS_TENSOR(expr, loc)                    \
    do {                                                \
      if (!expr->getType().isTensor()) {                \
        std::stringstream errorStr;                     \
        errorStr << "expected tensor";                  \
        REPORT_ERROR(errorStr.str(), loc);              \
      }                                                 \
    } while (0)

  #define CHECK_TYPE_EQUALITY(t1, t2, loc)              \
    do {                                                \
      if (!compare(t1, t2, ctx)) {                      \
        REPORT_TYPE_MISSMATCH(t1, t2, loc);             \
      }                                                 \
    } while (0)


  #define BINARY_ELWISE_TYPE_CHECK(lt, rt, loc)   \
    do {                                          \
      assert(lt.isTensor() && rt.isTensor());     \
      const TensorType *ltt = lt.toTensor();      \
      const TensorType *rtt = rt.toTensor();      \
      if (ltt->order() > 0 && rtt->order() > 0) { \
        CHECK_TYPE_EQUALITY(lt, rt, loc);         \
      }                                           \
    }                                             \
    while (0)
}

%union {
  // Primitive literals
  int         num;
  double      fnum;
  const char *string;
}
%destructor {} <num> <fnum>
%destructor { free((void*)($$)); } <string>
%destructor { delete $$; } <*>


// Tokens
%token UNKNOWN

// Literals
%right    "="
%nonassoc "\\"               // solve
%left     "==" "!="
%left     "<" ">" "<=" ">="
%left     "-" "+"
%left     "*" "/"
%left     NEG                // negation
%right    "^"                // exponentiation
%right    "'"                // transpose

%token <num>    INT_LITERAL    "int literal"
%token <fnum>   FLOAT_LITERAL  "float literal"
%token <string> STRING_LITERAL "string literal"
%token <string> IDENT          "identifier"

// Keywords
%token INT       "int"
%token FLOAT     "float"
%token TENSOR    "tensor"
%token ELEMENT   "element"
%token SET       "set"
%token CONST     "const"
%token EXTERN    "extern"
%token PROC      "proc"
%token FUNC      "func"
%token MAP       "map"
%token TO        "to"
%token WITH      "with"
%token REDUCE    "reduce"
%token WHILE     "while"
%token IF        "if"
%token ELIF      "elif"
%token ELSE      "else"
%token FOR       "for"
%token IN        "in"
%token BLOCKEND  "end"
%token RETURN    "return"
%token TEST      "%!"

%token RARROW    "->"
%token LP        "("
%token RP        ")"
%token LB        "["
%token RB        "]"
%token LC        "{"
%token RC        "}"
%token LA        "<"
%token RA        ">"
%token COMMA     ","
%token PERIOD    "."
%token COL       ":"
%token SEMICOL   ";"

%token ASSIGN    "="
%token PLUS      "+"
%token MINUS     "-"
%token STAR      "*"
%token SLASH     "/"
%token DOTSTAR   ".*"
%token DOTSLASH  "./"
%token EXP       "^"
%token TRANSPOSE "'"
%token BACKSLASH "\\"

%token EQ        "=="
%token NE        "!="
%token LE        "<="
%token GE        ">="

// End of file
%token END 0     "end of file"

%start program
%%

%union {
  std::shared_ptr<ir::Expression>              *expression;
  std::vector<std::shared_ptr<ir::Expression>> *expressions;
};

program
  : /* empty */
  | program program_element

%type<expression> program_element;
program_element
  : element_type_decl
  | procedure {
    std::unique_ptr<Function> function($procedure);
    std::string name = function->getName();
    if (ctx->containsFunction(name)) {
      REPORT_ERROR("procedure redefinition (" + name + ")", @procedure);
    }
    ctx->addFunction(function.release());
  }
  | function {
    std::unique_ptr<Function> function($function);
    std::string name = function->getName();
    if (ctx->containsFunction(name)) {
      REPORT_ERROR("function redefinition (" + name + ")", @function);
    }
    ctx->addFunction(function.release());
  }
  | extern {
    $$ = NULL;
  }
  | stmt {
    $$ = NULL;
    delete $stmt;
  }
  | test {
    ctx->addTest($test);
  }

// Externs
%type <expression> extern;
extern
  : "extern" argument_decl ";" {
    auto externArgument = convertAndDelete($argument_decl);
    ctx->addExtern(externArgument);
  }


// Element Definitions
%union {
  std::map<std::string, ir::Type> *fields;
  std::pair<std::string, ir::Type> *field;
};

%type <type> element_type_decl;
element_type_decl
  : "element" IDENT field_decl_list "end" {
    string name = convertAndFree($IDENT);
    unique_ptr<std::map<string,Type>> fields($field_decl_list);

    if (ctx->containsElementType(name)) {
      REPORT_ERROR("struct redefinition (" + name + ")", @element_type_decl);
    }

    ctx->addElementType(ElementType::make(name, *fields));
  }

%type <fields> field_decl_list;
field_decl_list
  : /* empty */ {
    $$ = new map<string,Type>;
  }
  | field_decl_list field_decl {
    $$ = $1;
    $$->insert(*$field_decl);
    delete $field_decl;
  }

%type <field> field_decl;
field_decl
  : IDENT ":" tensor_type  ";" {
    std::string name = convertAndFree($IDENT);
    auto tensorType = convertAndDelete($tensor_type);
    $$ = new pair<string,Type>(name, tensorType);
  }


// Procedures and functions
%union {
  ir::Function *function;

  std::shared_ptr<ir::Argument>              *argument;
  std::vector<std::shared_ptr<ir::Argument>> *arguments;

  std::shared_ptr<ir::Result>                *result;
  std::vector<std::shared_ptr<ir::Result>>   *results;
};

%type <function> procedure;
procedure
  : {ctx->scope();} procedure_header stmt_block {ctx->unscope();} BLOCKEND {
    auto statements = unique_ptr<vector<shared_ptr<Expression>>>($stmt_block);
    $$ = $procedure_header;
    $$->addStatements(*statements);
  }

%type <function> procedure_header;
procedure_header
  : "proc" IDENT {
    std::string name = convertAndFree($IDENT);
    auto arguments = vector<shared_ptr<Argument>>();
    auto results = vector<shared_ptr<Result>>();

    for (auto &extPair : ctx->getExterns()) {
      auto ext = extPair.second;

      // TODO: Replace extResult with mutable parameters
      auto tmp = new Result(ext->getName(), ext->getType());
      auto extResult = std::shared_ptr<Result>(tmp);
      results.push_back(extResult);

      arguments.push_back(ext);
      ctx->addSymbol(ext->getName(), ext, extResult);
    }

    $$ = new Function(name, arguments, results);
  }

%type <function> function;
function
  : {ctx->scope();} function_header stmt_block {ctx->unscope();} BLOCKEND{
    auto statements = unique_ptr<vector<shared_ptr<Expression>>>($stmt_block);
    $$ = $function_header;
    $$->addStatements(*statements);
  }

%type <function> function_header;
function_header
  : "func" IDENT "(" arguments ")" results {
    std::string name = convertAndFree($IDENT);
    auto arguments = unique_ptr<vector<shared_ptr<Argument>>>($arguments);
    auto results = unique_ptr<vector<shared_ptr<Result>>>($results);
    $$ = new Function(name, *arguments, *results);

    std::set<std::string> argumentNames;
    for (auto argument : *arguments) {
      ctx->addSymbol(argument->getName(), argument, NULL);
      argumentNames.insert(argument->getName());
    }

    for (auto result : *results) {
      std::shared_ptr<Expression> readExpr(NULL);
      if (argumentNames.find(result->getName()) != argumentNames.end()) {
        readExpr = ctx->getSymbol(result->getName()).getReadExpr();
      }

      ctx->addSymbol(result->getName(), readExpr, result);
    }
  }

%type <arguments> arguments;
arguments
  : /* empty */ {
    $$ = new vector<shared_ptr<Argument>>;
  }
  | argument_list {
    $$ = $1;
 }

%type <arguments> argument_list;
argument_list
  : argument_decl {
    auto argument = convertAndDelete($argument_decl);
    $$ = new vector<shared_ptr<Argument>>;
    $$->push_back(argument);
  }
  | argument_list "," argument_decl {
    auto argument = convertAndDelete($argument_decl);
    $$ = $1;
    $$->push_back(argument);
  }

%type <argument> argument_decl;
argument_decl
  : IDENT ":" type {
    std::string name = convertAndFree($IDENT);

    auto type = convertAndDelete($type);
    $$ = new shared_ptr<Argument>(new Argument(name, type));
  }

%type <results> results;
results
  : /* empty */ {
    $$ = new vector<shared_ptr<Result>>();
  }
  | RARROW "(" result_list ")" {
    $$ = $3;
  }

%type <results> result_list;
result_list
  : result_decl {
    auto result = convertAndDelete($result_decl);
    $$ = new vector<shared_ptr<Result>>;
    $$->push_back(result);
  }
  | result_list "," result_decl {
    auto result = convertAndDelete($result_decl);
    $$ = $1;
    $$->push_back(result);
  }

%type <result> result_decl;
result_decl
  : IDENT ":" type {
    std::string name = convertAndFree($IDENT);
    auto type = convertAndDelete($type);
    auto result = new Result(name, type);
    $$ = new shared_ptr<Result>(result);
  }


// Statements
%type<expressions> stmt_block;
stmt_block
  : /* empty */ {
    $$ = new vector<shared_ptr<Expression>>();
  }
  | stmt_block stmt {
    $$ = $1;
    if ($stmt == NULL) break;  // TODO: Remove check
    $$->push_back(*$stmt);
    delete $stmt;
  }

%type<expression> stmt;
stmt
  : const_stmt
  | assign_stmt
  | expr_stmt
  | if_stmt
  | for_stmt

%type<expression> const_stmt;
const_stmt
  : "const" IDENT ":" tensor_type "=" tensor_literal ";" {
    std::string ident = convertAndFree($IDENT);
    auto type = convertAndDelete($tensor_type);
    const TensorType *tensorType = type.toTensor();

    auto literal = shared_ptr<Literal>(*$tensor_literal);
    delete $tensor_literal;

    assert(literal->getType().isTensor() &&
           "Only tensor literals are currently supported");
    auto literalType = literal->getType();

    literal->setName(ident);

    // If tensor_type is a 1xn matrix and $tensor_literal is a vector then we
    // cast $tensor_literal to a 1xn matrix.
    const TensorType *literalTensorType = literalType.toTensor();
    if (tensorType->order() == 2 && literalTensorType->order() == 1) {
      literal->cast(type);
    }

    // Typecheck: value and literal types must be equivalent.
    CHECK_TYPE_EQUALITY(type, literal->getType(), @2);

    ctx->addSymbol(literal->getName(), literal);
    $$ = new shared_ptr<Expression>(literal);
  }

%type<expression> assign_stmt;
assign_stmt
  : write_expr_list "=" expr_list ";" {
    $$ = NULL;  // TODO: Remove this

    auto lhsList = unique_ptr<vector<unique_ptr<WriteInfo>>>($write_expr_list);
    auto rhsList = unique_ptr<vector<shared_ptr<Expression>>>($expr_list);

    if (lhsList->size() > rhsList->size()) {
      // TODO: Handle maps and then reintroduce this error
      // REPORT_ERROR("too few expressions assigned to too many variables", @2);
      break;
    }
    else if (lhsList->size() < rhsList->size()) {
      REPORT_ERROR("too many expressions assigned to too few variables", @2);
    }

    auto lhsIter = lhsList->begin();
    auto rhsIter = rhsList->begin();
    for (; lhsIter != lhsList->end(); ++lhsIter, ++rhsIter) {
      const unique_ptr<WriteInfo>  &lhs = *lhsIter;
      const shared_ptr<Expression> &rhs = *rhsIter;

      if (rhs == NULL) continue;  // TODO: Remove check

      assert(lhs->kind == WriteInfo::VARIABLE ||
             lhs->kind == WriteInfo::FIELD || lhs->kind == WriteInfo::TENSOR);
      switch (lhs->kind) {
        case WriteInfo::VARIABLE: {
          std::string variableName = *lhs->variableName;

          // TODO: Remove check
          if (!ctx->hasSymbol(variableName)) continue;

          assert(ctx->hasSymbol(variableName));

          const RWExprPair &varExprPair = ctx->getSymbol(variableName);
          assert(varExprPair.isWritable());

          shared_ptr<Expression> lhsTensor = varExprPair.getWriteExpr();
          auto result = dynamic_pointer_cast<Result>(lhsTensor);
          if (result) {
            CHECK_TYPE_EQUALITY(result->getType(), rhs->getType(), @2);
            rhs->setName(result->getName());
            result->addValue(rhs);
            $$ = new std::shared_ptr<Expression>(rhs);
          }
          else {
            NOT_SUPPORTED_YET;
          }
          break;
        }
        case WriteInfo::FIELD: {
          std::shared_ptr<FieldWrite> fieldWrite(*lhs->fieldWrite);
          fieldWrite->setValue(rhs);

          auto result = dynamic_pointer_cast<Result>(fieldWrite->getTarget());
          if (result) {
            result->addValue(fieldWrite);
            $$ = new std::shared_ptr<Expression>(fieldWrite);
          }
          else {
            NOT_SUPPORTED_YET;
          }

          break;
        }
        case WriteInfo::TENSOR: {
          std::shared_ptr<TensorWrite> tensorWrite(*lhs->tensorWrite);
          tensorWrite->setValue(rhs);

          auto result = dynamic_pointer_cast<Result>(tensorWrite->getTensor());
          if (result){
            result->addValue(tensorWrite);
            $$ = new std::shared_ptr<Expression>(tensorWrite);
          }
          else {
            NOT_SUPPORTED_YET;
          }

          break;
        }
      }
    }
  }

%type<expression> expr_stmt;
expr_stmt
  : expr ";" {
    $$ = NULL;
  }
  | ";" {
    $$ = NULL;
  }

%type <expression> if_stmt;
if_stmt
  : "if" expr stmt_block else_clauses BLOCKEND {
    $$ = NULL;
    delete $2;
    delete $3;
  }

else_clauses
  : /* empty */
  | elif_clauses "else" stmt_block {
    delete $3;
  }

elif_clauses
  : /* empty */
  | elif_clauses "elif" expr stmt_block {
    delete $3;
    delete $4;
  }

%type <expression> for_stmt;
for_stmt
  : "for" IDENT "in" index_set stmt_block BLOCKEND {
    $$ = NULL;
  }

// Expressions
%type <expression> expr;
expr
  : ident_expr
  | paren_expr
  | literal_expr
  | linear_algebra_expr
  | boolean_expr
  | field_read_expr
  | call_or_paren_read_expr
  | map_expr


// Read Ident Expression
%type <expression> ident_expr;
ident_expr
  : IDENT {
    string ident = convertAndFree($IDENT);

    if (!ctx->hasSymbol(ident)) { $$=NULL; break; } // TODO: Remove check

    if (!ctx->hasSymbol(ident)) {
      REPORT_ERROR(ident + " is not defined in scope", @1);
    }

    const RWExprPair &rwExprPair = ctx->getSymbol(ident);
    if (!rwExprPair.isReadable()) {
      REPORT_ERROR(ident + " is not readable", @1);
    }

    $$ = new shared_ptr<Expression>(rwExprPair.getReadExpr());
  }


// Parenthesis Expression
%type <expression> paren_expr;
paren_expr
  : "(" expr ")" {
    if ($2 == NULL) { $$ = NULL; break; } // TODO: Remove check
    $$ = $2;
  }


// Linear Algebra Expressions
%union {
  ir::IndexExpr::Operator         binop;
  std::shared_ptr<ir::IndexExpr> *indexExpr;

};
%destructor {} <binop>;

%type <indexExpr> linear_algebra_expr;
linear_algebra_expr
  : "-" expr %prec NEG {
    if ($2 == NULL) { $$ = NULL; break; } // TODO: Remove check

    std::shared_ptr<Expression> expr = convertAndDelete($2);
    CHECK_IS_TENSOR(expr, @2);

    $$ = new shared_ptr<IndexExpr>(unaryElwiseExpr(IndexExpr::NEG, expr));
  }
  | expr elwise_binary_op expr {  // + - .* ./
    if ($1 == NULL || $3 == NULL) { $$ = NULL; break; } // TODO: Remove check

    std::shared_ptr<Expression> l = convertAndDelete($1);
    std::shared_ptr<Expression> r = convertAndDelete($3);

    CHECK_IS_TENSOR(l, @1);
    CHECK_IS_TENSOR(r, @3);

    BINARY_ELWISE_TYPE_CHECK(l->getType(), r->getType(), @2);
    $$ = new shared_ptr<IndexExpr>(binaryElwiseExpr(l, $2, r));
  }
  | expr "*" expr {
    if ($1 == NULL || $3 == NULL) { $$ = NULL; break; } // TODO: Remove check

    std::shared_ptr<Expression> l = convertAndDelete($1);
    std::shared_ptr<Expression> r = convertAndDelete($3);

    CHECK_IS_TENSOR(l, @1);
    CHECK_IS_TENSOR(r, @3);

    const TensorType *ltype = l->getType().toTensor();
    const TensorType *rtype = r->getType().toTensor();

    // Scale
    if (ltype->order()==0 || rtype->order()==0) {
      $$ = new shared_ptr<IndexExpr>(binaryElwiseExpr(l, IndexExpr::MUL, r));
    }
    // Vector-Vector Multiplication (inner and outer product)
    else if (ltype->order() == 1 && rtype->order() == 1) {
      // Inner product
      if (!ltype->isColumnVector) {
        if (!rtype->isColumnVector) {
          REPORT_ERROR("cannot multiply two row vectors", @2);
        }
        if (l->getType() != r->getType()) {
          REPORT_TYPE_MISSMATCH(l->getType(), r->getType(), @2);
        }
        $$ = new shared_ptr<IndexExpr>(innerProduct(l, r));
      }
      // Outer product (l is a column vector)
      else {
        if (rtype->isColumnVector) {
          REPORT_ERROR("cannot multiply two column vectors", @2);
        }
        if (l->getType() != r->getType()) {
          REPORT_TYPE_MISSMATCH(l->getType(), r->getType(), @2);
        }
        $$ = new shared_ptr<IndexExpr>(outerProduct(l, r));
      }
    }
    // Matrix-Vector
    else if (ltype->order() == 2 && rtype->order() == 1) {
      if (ltype->dimensions[1] != rtype->dimensions[0]){
        REPORT_TYPE_MISSMATCH(l->getType(), r->getType(), @2);
      }
      $$ = new shared_ptr<IndexExpr>(gemv(l, r));
    }
    // Vector-Matrix
    else if (ltype->order() == 1 && rtype->order() == 2) {
      if (ltype->dimensions[0] != rtype->dimensions[0]){
        REPORT_TYPE_MISSMATCH(l->getType(), r->getType(), @2);
      }
      $$ = new shared_ptr<IndexExpr>(gevm(l,r));
    }
    // Matrix-Matrix
    else if (ltype->order() == 2 && rtype->order() == 2) {
      if (ltype->dimensions[1] != rtype->dimensions[0]){
        REPORT_TYPE_MISSMATCH(l->getType(), r->getType(), @2);
      }
      $$ = new shared_ptr<IndexExpr>(gemm(l,r));
    }
    else {
      REPORT_ERROR("cannot multiply >2-order tensors using *", @2);
      $$ = NULL;
    }
  }
  | expr "/" expr {
    if ($1 == NULL || $3 == NULL) { $$ = NULL; break; } // TODO: Remove check
    $$ = NULL;
    delete $1;
    delete $3;
  }
  | expr "'" {
    if ($1 == NULL) { $$ = NULL; break; } // TODO: Remove check

    auto expr = shared_ptr<Expression>(*$1);
    delete $1;

    CHECK_IS_TENSOR(expr, @1);

    const TensorType *type = expr->getType().toTensor();

    switch (type->order()) {
      case 0:
        // OPT: This might lead to redundant code to be removed in later pass
        $$ = new shared_ptr<IndexExpr>(unaryElwiseExpr(IndexExpr::NONE, expr));
        break;
      case 1:
        // OPT: This might lead to redundant code to be removed in later pass
        $$ = new shared_ptr<IndexExpr>(unaryElwiseExpr(IndexExpr::NONE, expr));
        if (!type->isColumnVector) {
          transposeVector(*$$);
        }
        break;
      case 2:
        $$ = new shared_ptr<IndexExpr>(transposeMatrix(expr));
        break;
      default:
        REPORT_ERROR("cannot transpose >2-order tensors using '", @1);
        $$ = NULL;
    }
  }
  | expr "^" expr {
    $$ = NULL;
    delete $1;
    delete $3;
  }
  | expr "\\" expr {  // Solve
    if ($1 == NULL || $3 == NULL) { $$ = NULL; break; } // TODO: Remove check
    $$ = NULL;
    delete $1;
    delete $3;
  }

%type <binop> elwise_binary_op;
elwise_binary_op
  : "+"  { $$ = IndexExpr::ADD; }
  | "-"  { $$ = IndexExpr::SUB; }
  | ".*" { $$ = IndexExpr::MUL; }
  | "./" { $$ = IndexExpr::DIV; }


// Boolean Expressions
%type <expression> boolean_expr;
boolean_expr
  : expr "==" expr {
    if ($1 == NULL || $3 == NULL) { $$ = NULL; break; } // TODO: Remove check
    $$ = NULL;
    delete $1;
    delete $3;
  }
  | expr "!=" expr {
    if ($1 == NULL || $3 == NULL) { $$ = NULL; break; } // TODO: Remove check
    $$ = NULL;
    delete $1;
    delete $3;
  }
  | expr "<"  expr {
    if ($1 == NULL || $3 == NULL) { $$ = NULL; break; } // TODO: Remove check
    $$ = NULL;
    delete $1;
    delete $3;
  }
  | expr ">"  expr {
    if ($1 == NULL || $3 == NULL) { $$ = NULL; break; } // TODO: Remove check
    $$ = NULL;
    delete $1;
    delete $3;
  }
  | expr "<=" expr {
    if ($1 == NULL || $3 == NULL) { $$ = NULL; break; } // TODO: Remove check
    $$ = NULL;
    delete $1;
    delete $3;
  }
  | expr ">=" expr {
    if ($1 == NULL || $3 == NULL) { $$ = NULL; break; } // TODO: Remove check
    $$ = NULL;
    delete $1;
    delete $3;
  }


// Field Read Expressions
%union {
  std::shared_ptr<ir::FieldRead> *fieldRead;
};

%type <fieldRead> field_read_expr;
field_read_expr
  : set_read_expr "." IDENT {
    if ($1 == NULL || $3 == NULL) { $$ = NULL; break; } // TODO: Remove check
    if (!(*$1)->getType().defined()) { $$ = NULL; break; } // TODO: Remove check

    std::shared_ptr<Expression> elemOrSet = convertAndDelete($set_read_expr);
    std::string fieldName = convertAndFree($IDENT);

    Type type = elemOrSet->getType();
    if (!(type.isElement() || type.isSet())) {
      std::stringstream errorStr;
      errorStr << "only elements and sets have fields";
      REPORT_ERROR(errorStr.str(), @1);
    }

    $$ = new shared_ptr<FieldRead>(new FieldRead(elemOrSet, fieldName));
  }

%type <expression> set_read_expr;
set_read_expr
  : ident_expr
  | paren_expr
  | call_expr


// Call and Tensor Read Expressions
%union {
  std::shared_ptr<ir::Call>       *call;
  std::shared_ptr<ir::TensorRead> *tensorRead;
};

%type <expression> call_or_paren_read_expr;
call_or_paren_read_expr
  : IDENT "(" expr_list_or_empty ")" {
    std::string name = convertAndFree($1);
    auto indices =
        unique_ptr<vector<shared_ptr<Expression>>>($expr_list_or_empty);

    if (ctx->hasSymbol(name)) {
      const RWExprPair &exprPair = ctx->getSymbol(name);
      if (!exprPair.isReadable()) {
        REPORT_ERROR(name + " is not readable", @1);
      }

      // The parenthesis read can read from a tensor or a tuple.
      auto expr = exprPair.getReadExpr();
      if (expr->getType().isTensor()) {
        $$ = new shared_ptr<Expression>(new TensorRead(expr, *indices));
      }
      else if (expr->getType().isTuple()) {
        if (indices->size() != 1) {
          REPORT_ERROR("reading a tuple requires exactly one index", @3);
        }

        $$ = new shared_ptr<Expression>(new TupleRead(expr, (*indices)[0]));
      }
      else {
        REPORT_ERROR("can only access components in tensors and tuples", @1);
      }
    }
    else if (ctx->containsFunction(name)) {
      $$ = NULL;
    }
    else {
      REPORT_ERROR(name + " is not defined in scope", @1);
    }
  }
  | field_read_expr "(" expr_list_or_empty ")" {
    $$ = NULL;
  }

%type<call> call_expr;
call_expr
  : IDENT "(" expr_list_or_empty ")" {
    std::string name = convertAndFree($1);
    auto actuals =
        unique_ptr<vector<shared_ptr<Expression>>>($expr_list_or_empty);
    auto call = new Call(name, *actuals);
    $$ = new std::shared_ptr<Call>(call);
  }

%type <expressions> expr_list_or_empty;
expr_list_or_empty
  : /* empty */ {
    $$ = new vector<shared_ptr<Expression>>();
  }
  | expr_list {
    $$ = $expr_list;
  }

%type <expressions> expr_list;
expr_list
  : expr {
    $$ = new std::vector<std::shared_ptr<Expression>>();
    if ($expr == NULL) break;  // TODO: Remove check
    $$->push_back(*$expr);
    delete $expr;
  }
  | expr_list "," expr {
    $$ = $1;
    if ($expr == NULL) break;  // TODO: Remove check
    $$->push_back(*$expr);
    delete $expr;
  }


// Map Expressions
%type <expression> map_expr;
map_expr
  : "map" IDENT "to" IDENT with reduce {
    string function($2);
    string target($4);
    free((void*)$2);
    free((void*)$4);
    $$ = NULL;
  }

with
  : /* empty */
  | "with" IDENT {
    std::string neighbor = convertAndFree($IDENT);
  }

reduce
  : /* empty */
  | "reduce" reduction_op

reduction_op
  : "+"
  | "*"


// Write Expressions
%code requires {
  struct WriteInfo {
    enum Kind { VARIABLE, FIELD, TENSOR };
    Kind kind;
    union {
      std::string                             *variableName;
      std::shared_ptr<simit::ir::FieldWrite>  *fieldWrite;
      std::shared_ptr<simit::ir::TensorWrite> *tensorWrite;
    };
    WriteInfo(const std::string &variableName) : kind(VARIABLE) {
      this->variableName = new std::string(variableName);
    }
    WriteInfo(std::shared_ptr<simit::ir::FieldWrite> *write) : kind(FIELD) {
      this->fieldWrite = write;
    }
    WriteInfo(std::shared_ptr<simit::ir::TensorWrite> *write) : kind(TENSOR) {
      this->tensorWrite = write;
    }

    ~WriteInfo() {
      switch (kind) {
        case Kind::VARIABLE:
          delete variableName;
          break;
        case Kind::FIELD:
          delete fieldWrite;
          break;
        case Kind::TENSOR:
          delete tensorWrite;
          break;
      }
    }
  };
};

%union {
  std::vector<std::unique_ptr<WriteInfo>> *writeinfos;
  WriteInfo                        *writeinfo;
  std::shared_ptr<ir::FieldWrite>  *fieldWrite;
  std::shared_ptr<ir::TensorWrite> *tensorWrite;
};

%type<writeinfos> write_expr_list;
write_expr_list
  : write_expr {
    $$ = new vector<unique_ptr<WriteInfo>>;
    if ($write_expr == NULL) break;  // TODO: Remove check
    $$->push_back(unique_ptr<WriteInfo>($write_expr));
  }
  | write_expr_list "," write_expr {
    $$ = $1;
    if ($write_expr == NULL) break;  // TODO: Remove check
    $$->push_back(unique_ptr<WriteInfo>($write_expr));
  }

%type <writeinfo> write_expr;
write_expr
  : IDENT {
    std::string name = convertAndFree($IDENT);
    $$ = new WriteInfo(name);
  }
  | field_write_expr {
    $$ = new WriteInfo($field_write_expr);
  }
  | tensor_write_expr {
    $$ = new WriteInfo($tensor_write_expr);
  }

%type <fieldWrite> field_write_expr;
field_write_expr
  : IDENT "." IDENT {
    string setName = convertAndFree($1);
    string fieldName = convertAndFree($3);

    if(!ctx->hasSymbol(setName)) { $$=NULL; break; } // TODO: Remove check

    if (!ctx->hasSymbol(setName)) {
      REPORT_ERROR(setName + " is not defined in scope", @1);
    }

    const RWExprPair &setExprPair = ctx->getSymbol(setName);
    if (!setExprPair.isWritable()) {
      REPORT_ERROR(setName + " is not writable", @1);
    }

    auto setExpr = setExprPair.getWriteExpr();
    $$ = new shared_ptr<FieldWrite>(new FieldWrite(setExpr, fieldName));
  }
  | paren_expr "." IDENT {
    $$ = NULL;
  }

%type <tensorWrite> tensor_write_expr;
tensor_write_expr
  : IDENT "(" expr_list ")" {
    std::string tensorName = convertAndFree($1);
    auto indices = unique_ptr<vector<shared_ptr<Expression>>>($expr_list);

    if(!ctx->hasSymbol(tensorName)) { $$=NULL; break; } // TODO: Remove check

    if (!ctx->hasSymbol(tensorName)) {
      REPORT_ERROR(tensorName + " is not defined in scope", @1);
    }

    const RWExprPair &tensorExprPair = ctx->getSymbol(tensorName);
    if (!tensorExprPair.isWritable()) {
      REPORT_ERROR(tensorName + " is not writable", @1);
    }

    auto tensorExpr = tensorExprPair.getWriteExpr();
    $$ = new shared_ptr<TensorWrite>(new TensorWrite(tensorExpr, *indices));
  }
  | field_write_expr "(" expr_list ")" {
    // TODO
    $$ = NULL;
  }


// Types
%union {
  ir::Type *type;

  std::vector<ir::IndexSet> *indexSets;
  ir::IndexSet              *indexSet;
};

%type <type> type;
type
  : element_type {
    $$ = $1;
  }
  | set_type {
    $$ = $1;
  }
  | tuple_type {
    $$ = $1;
  }
  | tensor_type {
    $$ = $1;
  }

%type <type> element_type;
element_type
  : IDENT {
    std::string name = convertAndFree($IDENT);
    $$ = new Type(ctx->getElementType(name));
  }

%type <type> set_type;
set_type
  : "set" "{" element_type "}" {
    auto elementType = convertAndDelete($element_type);
    $$ = new Type(SetType::make(elementType));
  }
  | "set" "{" element_type "}" "(" endpoints ")" {
    auto elementType = convertAndDelete($element_type);
    auto eps = convertAndDelete($endpoints);

    // TODO: Add endpoint information to set type
    $$ = new Type(SetType::make(elementType));
  }

%type <expressions> endpoints;
endpoints
  : IDENT {
    $$ = new vector<shared_ptr<Expression>>;
    std::string ident = convertAndFree($IDENT);
  }
  | endpoints "," IDENT {
    $$ = $1;
    std::string ident = convertAndFree($IDENT);
  }

%type <type> tuple_type;
tuple_type
  : "(" element_type "*" INT_LITERAL ")" {
    auto elementType = convertAndDelete($element_type);

    if ($INT_LITERAL<1) {
      REPORT_ERROR("Must be 1 or greater", @3);
    }

    $$ = new Type(TupleType::make(elementType, $INT_LITERAL));
  }

%type <type> tensor_type;
tensor_type
  : component_type {
    auto componentType = convertAndDelete($component_type);
    $$ = new Type(TensorType::make(componentType));
  }
  | "tensor" "(" tensor_type[block_type] ")" {
    $$ = $block_type;
  }
  | "tensor" "[" index_sets "]" "(" tensor_type[block_type] ")" {
    auto blockTypePtr = convertAndDelete($block_type);
    const TensorType *blockType = blockTypePtr.toTensor();

    auto componentType = blockType->componentType;

    auto outerDimensions = unique_ptr<vector<IndexSet>>($index_sets);
    auto blockDimensions = blockType->dimensions;

    vector<IndexDomain> dimensions;
    if (blockType->order() == 0) {
      for (size_t i=0; i<outerDimensions->size(); ++i) {
        dimensions.push_back(IndexDomain((*outerDimensions)[i]));
      }
    }
    else {
      // TODO: Handle the following cases where there there are more inner than
      //       outer dimensions (e.g. a vector of matrixes) and where there are
      //       more outer than inner dimensions (e.g. a matrix of vectors)
      //       gracefully by padding with '1'-dimensions.
      // TODO: Handle case where there are more block than outer dimensions
      // TODO: Handle case where there are more outer than block dimensions
      // TODO: Remove below error
      if (blockType->order() != outerDimensions->size()) {
        REPORT_ERROR("Blocktype order (" + to_string(blockType->order()) +
                     ") differ from number of dimensions", @index_sets);
      }

      assert(blockDimensions.size() == outerDimensions->size());
      for (size_t i=0; i<outerDimensions->size(); ++i) {
        vector<IndexSet> dimension;
        dimension.push_back((*outerDimensions)[i]);
        dimension.insert(dimension.begin(),
                         blockDimensions[i].getFactors().begin(),
                         blockDimensions[i].getFactors().end());

        dimensions.push_back(IndexDomain(dimension));
      }
    }

    $$ = new Type(TensorType::make(componentType, dimensions));
  }
  | tensor_type "'" {
    auto type = convertAndDelete($1);
    const TensorType *tensorType = type.toTensor();
    auto dimensions = tensorType->dimensions;
    auto componentType = tensorType->componentType;
    $$ = new Type(TensorType::make(componentType, dimensions, true));
  }

%type <indexSets> index_sets;
index_sets
  : index_set {
    $$ = new std::vector<IndexSet>();
    $$->push_back(*$index_set);
    delete $index_set;
  }
  | index_sets "," index_set {
    $$ = $1;
    $$->push_back(*$index_set);
    delete $index_set;
  }

%type <indexSet> index_set;
index_set
  : INT_LITERAL {
    $$ = new IndexSet($INT_LITERAL);
  }
  | IDENT {
    string ident = convertAndFree($IDENT);
    $$ = new IndexSet(ident);
  }
  | expr ":" expr {
    if ($1 == NULL || $3 == NULL) { $$ = NULL; break; } // TODO: Remove check
    $$ = NULL;
    delete $1;
    delete $3;
  }
  | "*" {
    $$ = new IndexSet();
  }

%type <type> component_type;
component_type
  : INT {
    $$ = new Type(Int(32));
  }
  | FLOAT {
    $$ = new Type(Float(64));
  }


// Literals
%code requires {
  namespace {
    template <typename T>
    class TensorValues {
     public:
      TensorValues() : dimSizes(1) {};

      void addValue(const T &val) {
        values.push_back(val);
        dimSizes[dimSizes.size()-1]++;
      }
      void addDimension() { dimSizes.push_back(1); }

      bool dimensionsMatch(const TensorValues<T> &other, std::string *errors) {
        assert(errors != NULL);
        std::string mismatchError = "missmatched dimension sizes";
        if (dimSizes.size()-1 != other.dimSizes.size()) {
          *errors = mismatchError;
          return false;
        }

        for (unsigned int i=0; i<dimSizes.size()-1; ++i) {
          if (dimSizes[i] != other.dimSizes[i]) {
            *errors = mismatchError;
            return false;
          }
        }
        return true;
      }

      void merge(const TensorValues<T> &other) {
        values.insert(values.end(), other.values.begin(), other.values.end());
        dimSizes[dimSizes.size()-1]++;
      }

      std::vector<unsigned int> dimSizes;
      std::vector<T>            values;
    };
  }
};

%union {
  std::shared_ptr<ir::Literal> *TensorLiteral;
  TensorValues<double>         *TensorDoubleValues;
  TensorValues<int>            *TensorIntValues;
};

%type <TensorLiteral> literal;
%type <TensorLiteral> tensor_literal;
%type <TensorLiteral> dense_tensor_literal;
%type <TensorLiteral> scalar_literal;

%type <TensorDoubleValues> float_dense_tensor_literal;
%type <TensorDoubleValues> float_dense_ndtensor_literal;
%type <TensorDoubleValues> float_dense_matrix_literal;
%type <TensorDoubleValues> float_dense_vector_literal;

%type <TensorIntValues> int_dense_tensor_literal;
%type <TensorIntValues> int_dense_ndtensor_literal;
%type <TensorIntValues> int_dense_matrix_literal;
%type <TensorIntValues> int_dense_vector_literal;

literal_expr
  : tensor_literal

tensor_literal
  : dense_tensor_literal
  | dense_tensor_literal "'" {
    $$ = $1;
    transposeVector(*$$);
  }
  | scalar_literal

dense_tensor_literal
  : "[" float_dense_tensor_literal "]" {
    auto values = unique_ptr<TensorValues<double>>($2);
    auto idoms = std::vector<IndexDomain>(values->dimSizes.rbegin(),
                                          values->dimSizes.rend());
    Type type = TensorType::make(Float(64), idoms);
    auto literal = new Literal(type, values->values.data());
    $$ = new shared_ptr<Literal>(literal);
  }
  | "[" int_dense_tensor_literal "]" {
    auto values = unique_ptr<TensorValues<int>>($2);
    auto idoms = std::vector<IndexDomain>(values->dimSizes.rbegin(),
                                          values->dimSizes.rend());
    Type type = TensorType::make(Int(32), idoms);
    auto literal = new Literal(type, values->values.data());
    $$ = new shared_ptr<Literal>(literal);
  }

float_dense_tensor_literal
  : float_dense_matrix_literal {
    // If the matrix has only one column then we discard that dimension and
    // treat it as a vector.
    if ($1->dimSizes[$1->dimSizes.size()-1] == 1) {
      $1->dimSizes.pop_back();
    }
  }
  | float_dense_ndtensor_literal

float_dense_ndtensor_literal
  : "[" float_dense_matrix_literal "]" {
    $$ = $2;
    $$->addDimension();
  }
  | float_dense_ndtensor_literal "," "[" float_dense_matrix_literal "]" {
    auto  left = unique_ptr<TensorValues<double>>($1);
    auto right = unique_ptr<TensorValues<double>>($4);

    string errorStr;
    if(!left->dimensionsMatch(*right, &errorStr)) {
      REPORT_ERROR(errorStr, @2);
    }
    left->merge(*right);
    $$ = left.release();
  }

float_dense_matrix_literal
  : float_dense_vector_literal {
    $$ = $1;
    $$->addDimension();
  }
  | float_dense_matrix_literal ";" float_dense_vector_literal {
    auto  left = unique_ptr<TensorValues<double>>($1);
    auto right = unique_ptr<TensorValues<double>>($3);

    string errorStr;
    if(!left->dimensionsMatch(*right, &errorStr)) {
      REPORT_ERROR(errorStr, @2);
    }

    left->merge(*right);
    $$ = left.release();
  }

float_dense_vector_literal
  : FLOAT_LITERAL {
    $$ = new TensorValues<double>();
    $$->addValue($1);
  }
  | float_dense_vector_literal "," FLOAT_LITERAL {
    $$ = $1;
    $$->addValue($3);
  }

int_dense_tensor_literal
  : int_dense_matrix_literal {
    // If the matrix has only one column then we discard that dimension and
    // treat it as a vector.
    if ($1->dimSizes[$1->dimSizes.size()-1] == 1) {
      $1->dimSizes.pop_back();
    }
  }
  | int_dense_ndtensor_literal

int_dense_ndtensor_literal
  : "[" int_dense_matrix_literal "]" {
    $$ = $2;
    $$->addDimension();
  }
  | int_dense_ndtensor_literal "," "[" int_dense_matrix_literal "]" {
    auto  left = unique_ptr<TensorValues<int>>($1);
    auto right = unique_ptr<TensorValues<int>>($4);

    string errorStr;
    if(!left->dimensionsMatch(*right, &errorStr)) {
      REPORT_ERROR(errorStr, @2);
    }
    left->merge(*right);
    $$ = left.release();
  }

int_dense_matrix_literal
  : int_dense_vector_literal {
    $$ = $1;
    $$->addDimension();
  }
  | int_dense_matrix_literal ";" int_dense_vector_literal {
    auto  left = unique_ptr<TensorValues<int>>($1);
    auto right = unique_ptr<TensorValues<int>>($3);

    string errorStr;
    if(!left->dimensionsMatch(*right, &errorStr)) {
      REPORT_ERROR(errorStr, @2);
    }

    left->merge(*right);
    $$ = left.release();
  }

int_dense_vector_literal
  : INT_LITERAL {
    $$ = new TensorValues<int>();
    $$->addValue($1);
  }
  | int_dense_vector_literal "," INT_LITERAL {
    $$ = $1;
    $$->addValue($3);
  }

scalar_literal
  : INT_LITERAL {
    auto scalarTensorType = TensorType::make(Int(32));
    auto literal = new Literal(scalarTensorType, &$1);
    $$ = new shared_ptr<Literal>(literal);
  }
  | FLOAT_LITERAL {
    auto scalarTensorType = TensorType::make(Float(64));
    auto literal = new Literal(scalarTensorType, &$1);
    $$ = new shared_ptr<Literal>(literal);
  }

// Tests
%union { ir::Test *test; };
%type <test> test;
test
  : "%!" call_expr "==" tensor_literal ";" {
    auto call = shared_ptr<Call>(*$call_expr);
    auto expected = shared_ptr<Literal>(*$tensor_literal);
    delete $call_expr;
    delete $tensor_literal;

    std::vector<std::shared_ptr<Literal>> literalArgs;
    literalArgs.reserve(call->getArguments().size());
    for (auto &arg : call->getArguments()) {
      std::shared_ptr<Literal> litarg = dynamic_pointer_cast<Literal>(arg);
      if (!litarg) {
        REPORT_ERROR("function calls in tests must have literal arguments", @1);
      }
      literalArgs.push_back(litarg);
    }
    assert(literalArgs.size() == call->getArguments().size());

    std::vector<std::shared_ptr<Literal>> expecteds;
    expecteds.push_back(expected);
    $$ = new Test(call->getName(), literalArgs, expecteds);
  }

%%
