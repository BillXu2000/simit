%require "3.0"
%language "C++"

%defines
%define api.namespace { simit::internal }
%define parser_class_name { Parser }
%define parse.error verbose
%locations
%no-lines

%code requires {
  namespace simit {
  namespace internal {
    class Scanner;
  }}
}

%parse-param { Scanner *scanner }
%parse-param { ParserParams *ctx };

%code {
  #include <stdlib.h>
  #include <cassert>
  #include <iostream>
  #include <algorithm>

  #include "scanner.h"
  #include "util.h"
  #include "indexvariables.h"
  using namespace std;
  using namespace simit;  // TODO: Remove
  using namespace simit::internal;

  #define REPORT_ERROR(msg, loc)  \
    do {                          \
      error(loc, msg);            \
      YYERROR;                    \
    } while (0)

  void Parser::error(const Parser::location_type &loc, const std::string &msg) {
    ctx->errors.push_back(Error(loc.begin.line, loc.begin.column,
                                loc.end.line, loc.end.column,
                                msg));
  }

  #undef yylex
  #define yylex scanner->lex
}

%code requires {
  #include <vector>

  #include "frontend.h"
  #include "ir.h"
  #include "errors.h"
  #include "types.h"

  namespace simit { namespace internal {
  struct ParserParams {
    ParserParams(SymbolTable             *symtable,
                 std::list<Function *>   *functions,
                 std::list<simit::Error> *errors,
                 std::list<Test>         *tests)
        : symtable(*symtable), functions(*functions), errors(*errors),
          tests(*tests) {}
    SymbolTable             &symtable;
    std::list<Function *>   &functions;
    std::list<simit::Error> &errors;
    std::list<Test>         &tests;
  };
  }}
}

%union {
  // Primitive literals
  int          num;
  double      fnum;
  const char *string;
}
%destructor {} <num> <fnum> <ComponentType>
%destructor { free((void*)($$)); } <string>
%destructor { delete $$; } <*>


// Tokens
%token UNKNOWN

// Literals
%right    "="
%nonassoc "\\"               // solve
%left     "==" "!="
%left     "<" ">" "<=" ">="
%left     "-" "+"
%left     "*" "/"
%left     NEG                // negation
%right    "^"                // exponentiation
%right    "'"                // transpose

%token <num>    INT_LITERAL    "int literal"
%token <fnum>   FLOAT_LITERAL  "float literal"
%token <string> STRING_LITERAL "string literal"
%token <string> IDENT          "identifier"

// Keywords
%token INT       "int"
%token FLOAT     "float"
%token STRUCT    "struct"
%token CONST     "const"
%token EXTERN    "extern"
%token PROC      "proc"
%token FUNC      "func"
%token TENSOR    "Tensor"
%token MAP       "map"
%token TO        "to"
%token WITH      "with"
%token REDUCE    "reduce"
%token WHILE     "while"
%token IF        "if"
%token ELIF      "elif"
%token ELSE      "else"
%token BLOCKEND  "end"
%token RETURN    "return"
%token TEST      "test"

%token RARROW    "->"
%token LP        "("
%token RP        ")"
%token LB        "["
%token RB        "]"
%token LC        "{"
%token RC        "}"
%token LA        "<"
%token RA        ">"
%token COMMA     ","
%token PERIOD    "."
%token COL       ":"
%token SEMICOL   ";"

%token ASSIGN    "="
%token PLUS      "+"
%token MINUS     "-"
%token STAR      "*"
%token SLASH     "/"
%token EXP       "^"
%token TRANSPOSE "'"
%token BACKSLASH "\\"

%token EQ        "=="
%token NE        "!="
%token LE        "<="
%token GE        ">="

// End of file
%token END 0     "end of file"

%start program
%%

%union {
  std::shared_ptr<simit::internal::IRNode>            *IRNode;
  std::list<std::shared_ptr<simit::internal::IRNode>> *IRNodes;
};

program
  : /* empty */
  | program program_element

%type<IRNode> program_element;
program_element
  : struct {
    $$ = NULL;
  }
  | procedure {
    $$ = NULL;
  }
  | function {
    ctx->functions.push_back($function);
  }
  | extern {
    $$ = NULL;
  }
  | stmt {
    $$ = NULL;
  }
  | test {
    $$ = NULL;
  }

// Externs
extern
  : "extern" IDENT type "{" "}" ";" {
    free((void*)$IDENT);
    delete $type;
  }
  | "extern" IDENT type "[" endpoints "]" "{" "}" ";" {
    free((void*)$IDENT);
    delete $type;
  }

endpoints
  : IDENT {
    free((void*)$IDENT);
  }
  | endpoints "," IDENT {
    free((void*)$IDENT);
  }


// Structs
struct
  : "struct" IDENT struct_decl_block {
    free((void*)$IDENT);
  }

struct_decl_block
  : /* empty */
  | struct_decl_list BLOCKEND

struct_decl_list
  : /* empty */
  | field_decl struct_decl_list

field_decl
  : var_decl  ";"


// Procedures and functions
%union {
  Function *Function;
};

%type <Function> procedure;
procedure
  : PROC IDENT stmt_block BLOCKEND {
    free((void*)$IDENT);
  }

%type <Function> function;
function
  : function_header stmt_block BLOCKEND {
    auto statements = unique_ptr<list<shared_ptr<IRNode>>>($stmt_block);
    $$ = $function_header;
    $$->addStatements(*statements);
    ctx->symtable.unscope();
  }

%type <Function> function_header;
function_header
  : FUNC IDENT "(" arguments ")" results {
    string ident($IDENT);
    free((void*)$IDENT);
    auto results = unique_ptr<list<shared_ptr<Result>>>($results);
    auto arguments = unique_ptr<list<shared_ptr<Argument>>>($arguments);

    $$ = new Function(ident, *arguments, *results);

    ctx->symtable.scope();
    for (auto argument : *arguments) {
      ctx->symtable.addNode(argument);
    }

    for (auto result : *results) {
      ctx->symtable.addNode(result);
    }
  }

%code requires {
  namespace {
  struct FormalData {
    std::string name;
    simit::internal::TensorType *type;
    FormalData(const std::string &name, simit::internal::TensorType *type)
        : name(name), type(type){}
  };
  }
};

%union {
  FormalData                           *Formal;
  std::list<FormalData*>               *Formals;
  std::list<std::shared_ptr<Argument>> *Arguments;
  std::list<std::shared_ptr<Result>>   *Results;
};
%destructor {
  for (auto formal : *$$) {
    delete formal;
  }
  delete $$;
} <Formals>;

%type <Arguments> arguments;
arguments
  : /* empty */ {
    $$ = new list<shared_ptr<Argument>>();
  }
  | formal_list {
    $$ = new list<shared_ptr<Argument>>();
    for (auto formal : *$formal_list) {
      auto result = new Argument(formal->name, formal->type);
      $$->push_back(shared_ptr<Argument>(result));
    }
    delete $formal_list;
 }

%type <Results> results;
results
  : /* empty */ {
    $$ = new list<shared_ptr<Result>>();
  }
  | RARROW "(" formal_list ")" {
    $$ = new list<shared_ptr<Result>>();
    for (auto formal : *$formal_list) {
      auto result = new Result(formal->name, formal->type);
      $$->push_back(shared_ptr<Result>(result));
    }
    delete $formal_list;
  }

%type <Formal> formal_decl;
formal_decl
  : IDENT ":" type {
    string ident($IDENT);
    free((void*)$IDENT);
    $$ = new FormalData(ident, $type);
  }

%type <Formals> formal_list;
formal_list
  : formal_decl {
    $$ = new list<FormalData *>();
    $$->push_back($formal_decl);
  }
  | formal_list "," formal_decl {
    $$->push_back($formal_decl);
  }

// Statements
%type<IRNodes> stmt_block;
stmt_block
  : /* empty */ {
    $$ = new list<shared_ptr<IRNode>>();
  }
  | stmt_block stmt {
    $$ = $1;
    if ($stmt == NULL) break;  // TODO: Remove check
    $$->insert($$->end(), $stmt->begin(), $stmt->end());
    delete $stmt;
  }

%type<IRNodes> stmt;
stmt
  : const_stmt
  | if_stmt
  | return_stmt
  | assign_stmt
  | expr_stmt

%type<IRNodes> const_stmt;
const_stmt
  : "const" IDENT ":" tensor_type "=" tensor_literal ";" {
    auto tensorType = unique_ptr<TensorType>($tensor_type);

    auto tensorLiteral = shared_ptr<LiteralTensor>(*$tensor_literal);
    delete $tensor_literal;

    tensorLiteral->setName($IDENT);
    free((void*)$IDENT);

    // If $type is a 1xn matrix and $tensor_literal is a vector then we cast
    // $tensor_literal to a 1xn matrix.
    if (tensorType->getOrder() == 2 && tensorLiteral->getOrder() == 1) {
      tensorLiteral->cast(tensorType.release());
    }

    // Typecheck: value and literal types must be equivalent.
    //            Note that the use of $tensor_type is deliberate as tensorType
    //            can have been released.
    if (*$tensor_type != *(tensorLiteral->getType())) {
      REPORT_ERROR("value and literal types do not match", @5);
    }

    ctx->symtable.addNode(tensorLiteral);

    $$ = new list<shared_ptr<IRNode>>();
    $$->push_back(tensorLiteral);
  }
  | "const" IDENT ":" element_type "=" element_literal ";" {
    free((void*)$IDENT);
    delete $element_type;
    $$ = NULL;
  }

%type<IRNodes> if_stmt;
if_stmt
  : "if" expr stmt_block else_clauses BLOCKEND {
    $$ = NULL;
  }

else_clauses
  : /* empty */
  | elif_clauses "else" stmt_block

elif_clauses
  : /* empty */
  | elif_clauses "elif" expr stmt_block

%type<IRNodes> return_stmt;
return_stmt
  : "return" ";" {
    $$ = NULL;
  }

%type<IRNodes> assign_stmt;
assign_stmt
  : lhs_expr_list "=" expr_list ";" {
    auto lhsList = unique_ptr<list<shared_ptr<Store>>>($lhs_expr_list);
    auto rhsList = unique_ptr<list<shared_ptr<TensorNode>>>($expr_list);

    $$ = new list<shared_ptr<IRNode>>();

    if (lhsList->size() > rhsList->size()) {
      // REPORT_ERROR("too few expressions assigned to too many variables", @2);
      break;
    }
    else if (lhsList->size() < rhsList->size()) {
      REPORT_ERROR("too mant expressions assigned to too few variables", @2);
    }

    auto lhsIter = lhsList->begin();
    auto rhsIter = rhsList->begin();
    for (; lhsIter != lhsList->end(); ++lhsIter, ++rhsIter) {
      auto lhs = *lhsIter;
      auto rhs = *rhsIter;

      if (rhs == NULL) continue;  // TODO: Remove this

      auto lhsTensor = ctx->symtable[lhs->getName()];
      if (lhsTensor != NULL) {
        if (auto result = dynamic_pointer_cast<Result>(lhsTensor)) {
          rhs->setName(result->getName());  // TODO: Generate unique name
          result->setValue(rhs);
          $$->push_back(rhs);
        }
      }
    }
  }

%type<IRNodes> expr_stmt;
expr_stmt
  : ";" {
    $$ = NULL;
  }
  | expr ";" {
    $$ = NULL;
  }


// Expressions
%union {
  std::shared_ptr<simit::internal::TensorNode>            *Tensor;
  std::list<std::shared_ptr<simit::internal::TensorNode>> *TensorList;
  std::shared_ptr<simit::internal::Store>                 *Store;
  std::list<std::shared_ptr<simit::internal::Store>>      *StoreList;
};

%type<Tensor> expr;
expr
  : IDENT {
    string ident($IDENT);
    free((void*)$IDENT);
    std::shared_ptr<IRNode> &node = ctx->symtable[ident];
    if (node == NULL) {
      // TODO: reintroduce error
      // REPORT_ERROR(ident + " is not defined in scope", @1);
      $$ = NULL;
      break;
    }

    shared_ptr<TensorNode> tensor = dynamic_pointer_cast<TensorNode>(node);
    if (tensor == NULL) {
      REPORT_ERROR(ident + " is not a tensor", @1);
    }

    $$ = new shared_ptr<TensorNode>(tensor);
  }
  | FLOAT_LITERAL {
    $$ = NULL;
  }
  | INT_LITERAL {
    $$ = NULL;
  }
  | STRING_LITERAL {
    $$ = NULL;
  }
  | expr "\\" expr {
    $$ = NULL;
  }
  | expr "+" expr {
    $$ = NULL;
  }
  | expr "-" expr {
    $$ = NULL;
  }
  | expr "*" expr {
    $$ = NULL;
  }
  | expr "/" expr {
    $$ = NULL;
  }
  | "-" expr %prec NEG {
    if ($2 == NULL) {
      $$ = NULL;
      break; // TODO: Remove check
    }

    auto expr = shared_ptr<TensorNode>(*$2);
    delete $2;

    IndexVarFactory indexVarFactory;
    auto indexVars = indexVarFactory.makeFreeVars(expr->getOrder());

    std::list<IndexExpr::IndexedTensor> operands;
    operands.push_front(IndexExpr::IndexedTensor(expr, indexVars));

    auto indexExpr = new IndexExpr(IndexExpr::NEG, indexVars, operands);
    assert(indexExpr != NULL);
    $$ = new shared_ptr<TensorNode>(indexExpr);
  }
  | expr "'" {
    $$ = NULL;
  }
  | expr "^" expr {
    $$ = NULL;
  }
  | expr "==" expr {
    $$ = NULL;
  }
  | expr "!=" expr {
    $$ = NULL;
  }
  | expr "<"  expr {
    $$ = NULL;
  }
  | expr ">"  expr {
    $$ = NULL;
  }
  | expr "<=" expr {
    $$ = NULL;
  }
  | expr ">=" expr {
    $$ = NULL;
  }
  | "(" expr ")" {
    $$ = NULL;
  }
  | expr ":" expr {
    $$ = NULL;
  }
  | call_expr {
    $$ = NULL;
  }
  | expr "." expr {
    $$ = NULL;
  }
  | index_expr {
    $$ = NULL;
  }
  | map_expr {
    $$ = NULL;
  }

call_expr
  : expr "(" ")"
  | expr "(" expr_list ")"

%type <TensorList> expr_list;
expr_list
  : expr {
    $$ = new list<shared_ptr<TensorNode>>();
    if ($expr == NULL) break;  // TODO: Remove check
    $$->push_back(*$expr);
    delete $expr;
  }
  | expr_list "," expr {
    $$ = $1;
    if ($expr == NULL) break;  // TODO: Remove check
    $$->push_back(*$expr);
    delete $expr;
  }

// Map expressions
map_expr
  : "map" IDENT[target] "to" IDENT[neighbor] with reduce {
    free((void*)$target);
    free((void*)$neighbor);
  }

with
  : /* empty */
  | "with" IDENT {
    free((void*)$IDENT);
  }

reduce
  : /* empty */
  | "reduce" reduction_op


// Index expressions
index_expr
  : reduction_indices expr

reduction_indices
  : /* empty */
  | reduction_indices reduction_index

reduction_index
  : reduction_op IDENT {
    free((void*)$IDENT);
  }
  | reduction_op "{" IDENT "}" {
    free((void*)$IDENT);
  }
  | IDENT[rfunc] "{" IDENT[rval] "}" {
    free((void*)$rfunc);
    free((void*)$rval);
  }

reduction_op
  : "+"
  | "*"


// Lhs expressions
%type<StoreList> lhs_expr_list;
lhs_expr_list
  : lhs_expr {
    $$ = new list<shared_ptr<Store>>();
    if ($lhs_expr == NULL) break;  // TODO: Remove check
    $$->push_back(*$lhs_expr);
    delete $lhs_expr;

  }
  | lhs_expr_list "," lhs_expr {
    $$ = $1;
    if ($lhs_expr == NULL) break;  // TODO: Remove check
    $$->push_back(*$lhs_expr);
    delete $lhs_expr;
  }

%type<Store> lhs_expr;
lhs_expr
  : IDENT {
    string ident($IDENT);
    free((void*)$IDENT);
    // TODO: Stores probably do not need to be TensorNode classes, but could be
    // some parser-internal class...
    auto variableStore = new VariableStore(ident, NULL);
    $$ = new std::shared_ptr<Store>(variableStore);
  }
  | IDENT "(" expr_list ")" {
    $$ = NULL;
  }
  | IDENT "." IDENT {
    $$ = NULL;
  }

// Variables
var_decl
  : IDENT ":" type {
    delete $type;
    free((void*)$IDENT);
  }


// Types
%union {
  TensorType                *TensorType;
  TensorType::ComponentType  ComponentType;
  std::vector<Shape*>       *Shapes;
  Shape                     *Shape;
  std::vector<Dimension*>   *Dimensions;
  Dimension                 *Dimension;
};

%type <TensorType>     type;
%type <TensorType>     tensor_type;
%type <Shapes>         shapes;
%type <Shape>          shape;
%type <Dimensions>     dimensions;
%type <Dimension>      dimension;
%type <ComponentType>  component_type;
%type <TensorType>     element_type;

type
  : element_type {
    $$ = $element_type;
  }
  | tensor_type {
    $$ = $tensor_type;
  }

element_type
  : IDENT {
    $$ = new ScalarType(TensorType::ELEMENT);
    free((void*)$IDENT);
  }

tensor_type
  : component_type {
    $$ = new ScalarType($component_type);
  }
  | TENSOR shapes "(" component_type ")" {
    auto shapes = unique_ptr<vector<Shape*>>($shapes);

    $$ = new ScalarType($component_type);
    typedef vector<Shape*>::reverse_iterator shapes_rit_t;
    for (shapes_rit_t rit = $shapes->rbegin(); rit != $shapes->rend(); ++rit) {
      $$ = new NDTensorType(*rit, $$);
    }
  }

shapes
  : /* empty */ {
    $$ = new vector<Shape*>();
  }
  | shapes shape {
    $$ = $1;
    $$->push_back($shape);
  }

shape
  : "[" dimensions "]" {
    $$ = new Shape(*$dimensions);
    delete $dimensions;
  }

dimensions
  : dimension {
    $$ = new vector<Dimension*>();
    $$->push_back($dimension);
  }
  | dimensions "," dimension {
    $$ = $1;
    $$->push_back($dimension);
  }

dimension
  : INT_LITERAL {
    $$ = new Dimension($INT_LITERAL);
  }
  | IDENT {
    free((void*)$IDENT);
    $$ = new Dimension(123456789);  // TODO: This needs to be a set dimension
  }
  | "*" {
    $$ = new Dimension();
  }

component_type
  : INT {
    $$ = ScalarType::INT;
  }
  | FLOAT {
    $$ = ScalarType::FLOAT;
  }


// Literals
%code requires {
  namespace {
    template <typename T>
    class TensorValues {
     public:
      TensorValues() : dimSizes(1) {};

      void addValue(const T &val) {
        values.push_back(val);
        dimSizes[dimSizes.size()-1]++;
      }
      void addDimension() { dimSizes.push_back(1); }

      bool dimensionsMatch(const TensorValues<T> &other, std::string *errors) {
        assert(errors != NULL);
        std::string mismatchError = "missmatched dimension sizes";
        if (dimSizes.size()-1 != other.dimSizes.size()) {
          *errors = mismatchError;
          return false;
        }

        for (unsigned int i=0; i<dimSizes.size()-1; ++i) {
          if (dimSizes[i] != other.dimSizes[i]) {
            *errors = mismatchError;
            return false;
          }
        }
        return true;
      }

      void merge(const TensorValues<T> &other) {
        values.insert(values.end(), other.values.begin(), other.values.end());
        dimSizes[dimSizes.size()-1]++;
      }

      std::vector<unsigned int> dimSizes;
      std::vector<T>            values;
    };
  }
};

%union {
  std::shared_ptr<LiteralTensor> *LiteralTensor;
  TensorValues<double>           *TensorDoubleValues;
  TensorValues<int>              *TensorIntValues;
};

%type        <Tensor> literal;
%type <LiteralTensor> tensor_literal;
%type <LiteralTensor> dense_tensor_literal;
%type <LiteralTensor> scalar_literal;

%type <TensorDoubleValues> float_dense_tensor_literal;
%type <TensorDoubleValues> float_dense_ndtensor_literal;
%type <TensorDoubleValues> float_dense_matrix_literal;
%type <TensorDoubleValues> float_dense_vector_literal;

%type <TensorIntValues> int_dense_tensor_literal;
%type <TensorIntValues> int_dense_ndtensor_literal;
%type <TensorIntValues> int_dense_matrix_literal;
%type <TensorIntValues> int_dense_vector_literal;

literal
  : element_literal
  | tensor_literal

element_literal
  :

tensor_literal
  : dense_tensor_literal
  | scalar_literal

%code {
  Shape *dimSizesToShape(const vector<unsigned int> &dimSizes) {
    auto dims = std::vector<Dimension*>();
    for (auto rit=dimSizes.rbegin(); rit!=dimSizes.rend(); ++rit) {
      dims.push_back(new Dimension(*rit));
    }
    return new Shape(dims);
  }
};

dense_tensor_literal
  : "[" float_dense_tensor_literal "]" {
    auto values = unique_ptr<TensorValues<double>>($2);
    Shape *shape = dimSizesToShape(values->dimSizes);
    auto type = new NDTensorType(shape, new ScalarType(ScalarType::FLOAT));
    auto literal = new LiteralTensor(type, values->values.data());
    $$ = new shared_ptr<LiteralTensor>(literal);
  }
  | "[" int_dense_tensor_literal "]" {
    auto values = unique_ptr<TensorValues<int>>($2);
    Shape *shape = dimSizesToShape(values->dimSizes);
    auto type = new NDTensorType(shape, new ScalarType(ScalarType::INT));
    auto literal = new LiteralTensor(type, values->values.data());
    $$ = new shared_ptr<LiteralTensor>(literal);
  }

float_dense_tensor_literal
  : float_dense_matrix_literal {
    // If the matrix has only one column then we discard that dimension and
    // treat it as a vector.
    if ($1->dimSizes[$1->dimSizes.size()-1] == 1) {
      $1->dimSizes.pop_back();
    }
  }
  | float_dense_ndtensor_literal

float_dense_ndtensor_literal
  : "[" float_dense_matrix_literal "]" {
    $$ = $2;
    $$->addDimension();
  }
  | float_dense_ndtensor_literal "," "[" float_dense_matrix_literal "]" {
    auto  left = unique_ptr<TensorValues<double>>($1);
    auto right = unique_ptr<TensorValues<double>>($4);

    string errorStr;
    if(!left->dimensionsMatch(*right, &errorStr)) {
      REPORT_ERROR(errorStr, @2);
    }
    left->merge(*right);
    $$ = left.release();
  }

float_dense_matrix_literal
  : float_dense_vector_literal {
    $$ = $1;
    $$->addDimension();
  }
  | float_dense_matrix_literal ";" float_dense_vector_literal {
    auto  left = unique_ptr<TensorValues<double>>($1);
    auto right = unique_ptr<TensorValues<double>>($3);

    string errorStr;
    if(!left->dimensionsMatch(*right, &errorStr)) {
      REPORT_ERROR(errorStr, @2);
    }

    left->merge(*right);
    $$ = left.release();
  }

float_dense_vector_literal
  : FLOAT_LITERAL {
    $$ = new TensorValues<double>();
    $$->addValue($1);
  }
  | float_dense_vector_literal "," FLOAT_LITERAL {
    $$ = $1;
    $$->addValue($3);
  }

int_dense_tensor_literal
  : int_dense_matrix_literal {
    // If the matrix has only one column then we discard that dimension and
    // treat it as a vector.
    if ($1->dimSizes[$1->dimSizes.size()-1] == 1) {
      $1->dimSizes.pop_back();
    }
  }
  | int_dense_ndtensor_literal

int_dense_ndtensor_literal
  : "[" int_dense_matrix_literal "]" {
    $$ = $2;
    $$->addDimension();
  }
  | int_dense_ndtensor_literal "," "[" int_dense_matrix_literal "]" {
    auto  left = unique_ptr<TensorValues<int>>($1);
    auto right = unique_ptr<TensorValues<int>>($4);

    string errorStr;
    if(!left->dimensionsMatch(*right, &errorStr)) {
      REPORT_ERROR(errorStr, @2);
    }
    left->merge(*right);
    $$ = left.release();
  }

int_dense_matrix_literal
  : int_dense_vector_literal {
    $$ = $1;
    $$->addDimension();
  }
  | int_dense_matrix_literal ";" int_dense_vector_literal {
    auto  left = unique_ptr<TensorValues<int>>($1);
    auto right = unique_ptr<TensorValues<int>>($3);

    string errorStr;
    if(!left->dimensionsMatch(*right, &errorStr)) {
      REPORT_ERROR(errorStr, @2);
    }

    left->merge(*right);
    $$ = left.release();
  }

int_dense_vector_literal
  : INT_LITERAL {
    $$ = new TensorValues<int>();
    $$->addValue($1);
  }
  | int_dense_vector_literal "," INT_LITERAL {
    $$ = $1;
    $$->addValue($3);
  }

scalar_literal
  : INT_LITERAL {
    auto scalarType = new ScalarType(ScalarType::INT);
    auto literal = new LiteralTensor(scalarType, &$1);
    $$ = new shared_ptr<LiteralTensor>(literal);
  }
  | FLOAT_LITERAL {
    auto scalarType = new ScalarType(ScalarType::FLOAT);
    auto literal = new LiteralTensor(scalarType, &$1);
    $$ = new shared_ptr<LiteralTensor>(literal);
  }

// Tests
%type<IRNodes> test;
test
  : TEST test_stmt_block {
    $$ = new list<shared_ptr<IRNode>>();
    $$->push_back(shared_ptr<IRNode>(new Test("MyTest")));
  }

test_stmt_block
  : /* empty */
  | test_stmt_block test_stmt

test_stmt
  : stmt
  | expr "->" literal ";" {
    auto expr = shared_ptr<TensorNode>(*$expr);
    auto literal = shared_ptr<TensorNode>(*$literal);
    delete $expr;
    delete $literal;
  }
%%
