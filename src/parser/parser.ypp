%require "3.0"
%language "C++"

%defines
%define api.namespace { simit::internal }
%define parser_class_name { Parser }
%define parse.error verbose
%locations

%code requires {
  namespace simit {
  namespace internal {
    class Scanner;
    class ProgramContext;
  }}

  #include <vector>

  #include "util.h"
  #include "ir.h"
  #include "errors.h"
  #include "types.h"
}

%parse-param { Scanner *scanner };
%parse-param { ProgramContext *ctx };
%parse-param { std::vector<Error> *errors };

%code {
  #include <stdlib.h>
  #include <cassert>
  #include <iostream>
  #include <map>
  #include <set>
  #include <algorithm>

  #include "program_context.h"
  #include "scanner.h"
  #include "irutils.h"
  using namespace std;
  using namespace simit::internal;

  std::string typeString(const Type &type, ProgramContext *ctx) {
    std::stringstream ss;
    ss << type;
    std::string str = ss.str();
    if (ctx->isColumnVector(type)) {
      str += "'";
    }
    return str;
  }

  #define REPORT_ERROR(msg, loc)  \
    do {                          \
      error((loc), (msg));        \
      YYERROR;                    \
    } while (0)

  #define REPORT_TYPE_MISSMATCH(t1, t2, loc)           \
    do {                                               \
      std::stringstream errorStr;                      \
      errorStr << "type missmatch ("                   \
               << typeString(t1, ctx) << " and "       \
               << typeString(t2, ctx) << ")";          \
      REPORT_ERROR(errorStr.str(), loc);               \
    } while (0)

  #define REPORT_INDEX_VARIABLE_MISSMATCH(numIndexVars, order, loc) \
    do {                                                            \
      REPORT_ERROR("wrong number of index variables (" +            \
                    to_string(numIndexVars) +                       \
                    " index variables, but tensor order is " +      \
                    to_string(order), loc);                         \
      } while (0)

  void Parser::error(const Parser::location_type &loc, const std::string &msg) {
    errors->push_back(Error(loc.begin.line, loc.begin.column,
                            loc.end.line, loc.end.column, msg));
  }

  #undef yylex
  #define yylex scanner->lex

  inline std::string convertAndFree(const char *str) {
    std::string result = std::string(str);
    free((void*)str);
    return result;
  }

  template <typename T>
  inline T convertAndDelete(T *obj) {
    auto result = T(*obj);
    delete obj;
    return result;
  }

  bool compare(const Type &l, const Type &r, ProgramContext *ctx) {
    if (l.getKind() != r.getKind()) {
      return false;
    }

    if (l.isTensor()) {
      if (ctx->isColumnVector(l) != ctx->isColumnVector(r)) {
        return false;
      }
    }

    if (l != r) {
      return false;
    }
    return true;
  }

  #define CHECK_IS_TENSOR(expr, loc)                    \
    do {                                                \
      if (!expr->getType()->isTensor()) {               \
        std::stringstream errorStr;                     \
        errorStr << "expected tensor";                  \
        REPORT_ERROR(errorStr.str(), loc);              \
      }                                                 \
    } while (0)



  #define CHECK_TYPE_EQUALITY(t1, t2, loc)              \
    do {                                                \
      if (!compare(t1, t2, ctx)) {                      \
        REPORT_TYPE_MISSMATCH(t1, t2, loc);             \
      }                                                 \
    } while (0)


  #define BINARY_ELWISE_TYPE_CHECK(lt, rt, loc)         \
    do {                                                \
      TensorType *ltt = tensorTypePtr(lt);              \
      TensorType *rtt = tensorTypePtr(rt);              \
      if (ltt->getOrder() > 0 && rtt->getOrder() > 0) { \
        CHECK_TYPE_EQUALITY(*ltt, *rtt, loc);           \
      }                                                 \
    }                                                   \
    while (0)
}

%union {
  // Primitive literals
  int         num;
  double      fnum;
  const char *string;
}
%destructor {} <num> <fnum>
%destructor { free((void*)($$)); } <string>
%destructor { delete $$; } <*>


// Tokens
%token UNKNOWN

// Literals
%right    "="
%nonassoc "\\"               // solve
%left     "==" "!="
%left     "<" ">" "<=" ">="
%left     "-" "+"
%left     "*" "/"
%left     NEG                // negation
%right    "^"                // exponentiation
%right    "'"                // transpose

%token <num>    INT_LITERAL    "int literal"
%token <fnum>   FLOAT_LITERAL  "float literal"
%token <string> STRING_LITERAL "string literal"
%token <string> IDENT          "identifier"

// Keywords
%token INT       "int"
%token FLOAT     "float"
%token STRUCT    "struct"
%token CONST     "const"
%token EXTERN    "extern"
%token PROC      "proc"
%token FUNC      "func"
%token TENSOR    "Tensor"
%token MAP       "map"
%token TO        "to"
%token WITH      "with"
%token REDUCE    "reduce"
%token WHILE     "while"
%token IF        "if"
%token ELIF      "elif"
%token ELSE      "else"
%token BLOCKEND  "end"
%token RETURN    "return"
%token TEST      "%!"

%token RARROW    "->"
%token LP        "("
%token RP        ")"
%token LB        "["
%token RB        "]"
%token LC        "{"
%token RC        "}"
%token LA        "<"
%token RA        ">"
%token COMMA     ","
%token PERIOD    "."
%token COL       ":"
%token SEMICOL   ";"

%token ASSIGN    "="
%token PLUS      "+"
%token MINUS     "-"
%token STAR      "*"
%token SLASH     "/"
%token DOTSTAR   ".*"
%token DOTSLASH  "./"
%token EXP       "^"
%token TRANSPOSE "'"
%token BACKSLASH "\\"

%token EQ        "=="
%token NE        "!="
%token LE        "<="
%token GE        ">="

// End of file
%token END 0     "end of file"

%start program
%%

%union {
  std::shared_ptr<simit::internal::IRNode>              *IRNode;
  std::vector<std::shared_ptr<simit::internal::IRNode>> *IRNodes;
};

program
  : /* empty */
  | program program_element

%type<IRNode> program_element;
program_element
  : element_type_decl {
    $$ = NULL;
    std::unique_ptr<ElementType> elementType($element_type_decl);

    std::string name = elementType->getName();
    if (ctx->containsElementType(name)) {
      REPORT_ERROR("struct redefinition (" + name + ")", @element_type_decl);
    }
    ctx->addElementType(elementType.release());
  }
  | procedure {
    $$ = NULL;
  }
  | function {
    std::unique_ptr<Function> function($function);

    std::string name = function->getName();
    if (ctx->containsFunction(name)) {
      REPORT_ERROR("function redefinition (" + name + ")", @function);
    }
    ctx->addFunction(function.release());
  }
  | extern {
    $$ = NULL;
  }
  | stmt {
    $$ = NULL;
    delete $stmt;
  }
  | test {
    ctx->addTest($test);
  }

// Externs
// TODO: Remove externs
extern
  : "extern" IDENT type ";" {
    std::string ident = convertAndFree($IDENT);
    delete $type;
  }
  | "extern" IDENT type ";" {
    std::string ident = convertAndFree($IDENT);
    delete $type;
  }

endpoints
  : IDENT {
    std::string ident = convertAndFree($IDENT);
  }
  | endpoints "," IDENT {
    std::string ident = convertAndFree($IDENT);
  }


// Element Definitions
%union {
  ElementType                *elementType;
  std::vector<ElementField*> *fields;
  ElementField               *field;
};

%type <elementType> element_type_decl;
element_type_decl
  : "struct" IDENT field_decl_list "end" {
    std::string name = convertAndFree($IDENT);
    std::unique_ptr<std::vector<ElementField*>> fields($field_decl_list);
    $$ = new ElementType(name, *fields);
  }

%type <fields> field_decl_list;
field_decl_list
  : /* empty */ {
    $$ = new std::vector<ElementField*>();
  }
  | field_decl_list field_decl {
    $$ = $1;
    $$->push_back($field_decl);
  }

%type <field> field_decl;
field_decl
  : IDENT ":" tensor_type  ";" {
    std::string name = convertAndFree($IDENT);
    $$ = new ElementField(name, $tensor_type);
  }

// Procedures and functions
%union {
  Function *function;
};

%type <function> procedure;
procedure
  : "proc" IDENT stmt_block BLOCKEND {
    std::string ident = convertAndFree($IDENT);
    delete $stmt_block;
  }

%type <function> function;
function
  : function_header stmt_block BLOCKEND {
    auto statements = unique_ptr<vector<shared_ptr<IRNode>>>($stmt_block);
    $$ = $function_header;
    $$->addStatements(*statements);
    ctx->unscope();
  }

%type <function> function_header;
function_header
  : "func" IDENT arguments results {
    std::string ident = convertAndFree($IDENT);
    auto arguments = unique_ptr<vector<shared_ptr<Argument>>>($arguments);
    auto results = unique_ptr<vector<shared_ptr<Result>>>($results);

    $$ = new Function(ident, *arguments, *results);

    ctx->scope();
    for (auto argument : *arguments) {
      ctx->addSymbol(argument->getName(), argument);
    }

    for (auto result : *results) {
      ctx->addSymbol(result->getName(), result);
    }
  }

%code requires {
  namespace {
  struct FormalData {
    std::string name;
    std::shared_ptr<simit::internal::Type> type;
    FormalData(const std::string &name,
               const std::shared_ptr<simit::internal::Type> &type)
        : name(name), type(type){}
  };
  }
};

%union {
  FormalData                                *Formal;
  simit::util::OwnershipVector<FormalData*> *Formals;
  std::vector<std::shared_ptr<Argument>>    *Arguments;
  std::vector<std::shared_ptr<Result>>      *Results;
};

%type <Arguments> arguments;
arguments
  : "(" ")" {
    $$ = new vector<shared_ptr<Argument>>();
  }
  | "(" formal_list ")" {
    $$ = new vector<shared_ptr<Argument>>();
    for (auto formal : *$formal_list) {
      auto result = new Argument(formal->name, formal->type);
      $$->push_back(shared_ptr<Argument>(result));
    }
    delete $formal_list;
 }

%type <Results> results;
results
  : /* empty */ {
    $$ = new vector<shared_ptr<Result>>();
    $$->push_back(shared_ptr<Result>(new Result("asd", NULL)));
  }
  | RARROW "(" formal_list ")" {
    $$ = new vector<shared_ptr<Result>>();
    for (auto formal : *$formal_list) {
      auto result = new Result(formal->name, formal->type);
      $$->push_back(shared_ptr<Result>(result));
    }
    delete $formal_list;
  }

%type <Formals> formal_list;
formal_list
  : formal_decl {
    $$ = new simit::util::OwnershipVector<FormalData *>();
    $$->push_back($formal_decl);
  }
  | formal_list "," formal_decl {
    $$->push_back($formal_decl);
  }

%type <Formal> formal_decl;
formal_decl
  : IDENT ":" type {
    std::string ident = convertAndFree($IDENT);
    $$ = new FormalData(ident, std::shared_ptr<Type>($type));
  }

// Statements
%type<IRNodes> stmt_block;
stmt_block
  : /* empty */ {
    $$ = new vector<shared_ptr<IRNode>>();
  }
  | stmt_block stmt {
    $$ = $1;
    if ($stmt == NULL) break;  // TODO: Remove check
    $$->insert($$->end(), $stmt->begin(), $stmt->end());
    delete $stmt;
  }

%type<IRNodes> stmt;
stmt
  : const_stmt
  | return_stmt
  | assign_stmt
  | expr_stmt
  | if_stmt

%type<IRNodes> const_stmt;
const_stmt
  : "const" IDENT ":" tensor_type "=" tensor_literal ";" {
    std::string ident = convertAndFree($IDENT);
    auto tensorType = std::shared_ptr<TensorType>($tensor_type);

    auto literal = shared_ptr<Literal>(*$tensor_literal);
    delete $tensor_literal;

    assert(literal->getType()->isTensor() && "Set literals not supported yet");
    auto literalType = literal->getType();

    literal->setName(ident);

    // If tensor_type is a 1xn matrix and $tensor_literal is a vector then we
    // cast $tensor_literal to a 1xn matrix.
    TensorType *literalTensorType = tensorTypePtr(literalType);
    if (tensorType->getOrder() == 2 && literalTensorType->getOrder() == 1) {
      literal->cast(tensorType);
    }

    // Typecheck: value and literal types must be equivalent.
    CHECK_TYPE_EQUALITY(*tensorType, *literal->getType(), @2);

    ctx->addSymbol(literal->getName(), literal);

    $$ = new vector<shared_ptr<IRNode>>();
    $$->push_back(literal);
  }

%type<IRNodes> return_stmt;
return_stmt
  : "return" ";" {
    $$ = NULL;
  }

%type<IRNodes> assign_stmt;
assign_stmt
  : lhs_expr_list "=" expr_list ";" {
    auto lhsList = unique_ptr<VariableAccessVector>($lhs_expr_list);
    auto rhsList = unique_ptr<vector<shared_ptr<Expression>>>($expr_list);

    $$ = new vector<shared_ptr<IRNode>>();

    if (lhsList->size() > rhsList->size()) {
      // TODO: Handle maps and then reintroduce this error
      // REPORT_ERROR("too few expressions assigned to too many variables", @2);
      break;
    }
    else if (lhsList->size() < rhsList->size()) {
      REPORT_ERROR("too many expressions assigned to too few variables", @2);
    }

    auto lhsIter = lhsList->begin();
    auto rhsIter = rhsList->begin();
    for (; lhsIter != lhsList->end(); ++lhsIter, ++rhsIter) {
      VariableAccess *lhs = *lhsIter;
      std::shared_ptr<Expression> &rhs = *rhsIter;

      // TODO: Remove these checks
      if (rhs == NULL) continue;
      if (!ctx->hasSymbol(lhs->name)) continue;

      assert(ctx->hasSymbol(lhs->name));
      shared_ptr<IRNode> lhsTensor = ctx->getSymbol(lhs->name);
      assert(dynamic_pointer_cast<Expression>(lhsTensor) != NULL);
      if (auto result = dynamic_pointer_cast<Result>(lhsTensor)) {
        CHECK_TYPE_EQUALITY(*result->getType(), *rhs->getType(), @2);
        rhs->setName(result->getName());
        result->setValue(rhs);
        $$->push_back(rhs);
      }
      else {
        NOT_SUPPORTED_YET;
      }
    }
  }

%type<IRNodes> expr_stmt;
expr_stmt
  : expr ";" {
    $$ = NULL;
  }
  | ";" {
    $$ = NULL;
  }

%type<IRNodes> if_stmt;
if_stmt
  : "if" expr stmt_block else_clauses BLOCKEND {
    $$ = NULL;
    delete $2;
    delete $3;
  }

else_clauses
  : /* empty */
  | elif_clauses "else" stmt_block {
    delete $3;
  }

elif_clauses
  : /* empty */
  | elif_clauses "elif" expr stmt_block {
    delete $3;
    delete $4;
  }


// Expressions
%union {
  std::shared_ptr<simit::internal::Expression>              *expression;
  std::vector<std::shared_ptr<simit::internal::Expression>> *expressions;
};

%type <expression> expr;
expr
  : IDENT {
    string ident = convertAndFree($IDENT);
    if (!ctx->hasSymbol(ident)) {
      // TODO: reintroduce error
      // REPORT_ERROR(ident + " is not defined in scope", @1);
      $$ = NULL;
      break;
    }
    const std::shared_ptr<IRNode> &node = ctx->getSymbol(ident);

    shared_ptr<Expression> tensor = dynamic_pointer_cast<Expression>(node);
    if (tensor == NULL) {
      REPORT_ERROR(ident + " is not a tensor", @1);
    }

    $$ = new shared_ptr<Expression>(tensor);
  }
  | literal
  | STRING_LITERAL {
    $$ = NULL;
  }
  | "-" expr %prec NEG {
    if ($2 == NULL) {  // TODO: Remove check
      $$ = NULL;
      break;
    }

    std::shared_ptr<Expression> expr = convertAndDelete($2);
    CHECK_IS_TENSOR(expr, @2);

    $$ = new shared_ptr<Expression>(unaryElwiseExpr(IndexExpr::NEG, expr));
  }
  // + - .* ./
  | expr elwise_binary_op expr {
    if ($1 == NULL || $3 == NULL) {  // TODO: Remove this check
      $$ = NULL;
      break;
    }
    std::shared_ptr<Expression> l = convertAndDelete($1);
    std::shared_ptr<Expression> r = convertAndDelete($3);

    CHECK_IS_TENSOR(l, @1);
    CHECK_IS_TENSOR(r, @3);

    BINARY_ELWISE_TYPE_CHECK(l->getType(), r->getType(), @2);
    $$ = new shared_ptr<Expression>(binaryElwiseExpr(l, $2, r));
  }
  | expr "*" expr {
    if ($1 == NULL || $3 == NULL) {  // TODO: Remove this check
      $$ = NULL;
      break;
    }

    std::shared_ptr<Expression> l = convertAndDelete($1);
    std::shared_ptr<Expression> r = convertAndDelete($3);

    CHECK_IS_TENSOR(l, @1);
    CHECK_IS_TENSOR(r, @3);

    TensorType *ltype = tensorTypePtr(l->getType());
    TensorType *rtype = tensorTypePtr(r->getType());

    // Scale
    if (ltype->getOrder()==0 || rtype->getOrder()==0) {
      $$ = new shared_ptr<Expression>(binaryElwiseExpr(l, IndexExpr::MUL, r));
    }
    // Vector-Vector Multiplication (inner and outer product)
    else if (ltype->getOrder() == 1 && rtype->getOrder() == 1) {
      // Inner product
      if (!ctx->isColumnVector(*l->getType())) {
        if (!ctx->isColumnVector(*r->getType())) {
          REPORT_ERROR("cannot multiply two row vectors", @2);
        }
        if (*l->getType() != *r->getType()) {
          REPORT_TYPE_MISSMATCH(*l->getType(), *r->getType(), @2);
        }
        $$ = new shared_ptr<Expression>(innerProduct(l, r));
      }
      // Outer product (l is a column vector)
      else {
        if (ctx->isColumnVector(*r->getType())) {
          REPORT_ERROR("cannot multiply two column vectors", @2);
        }
        if (*l->getType() != *r->getType()) {
          REPORT_TYPE_MISSMATCH(*l->getType(), *r->getType(), @2);
        }
        $$ = new shared_ptr<Expression>(outerProduct(l, r));
      }
    }
    // Matrix-Vector
    else if (ltype->getOrder() == 2 && rtype->getOrder() == 1) {
      if (ltype->getDimensions()[1] != rtype->getDimensions()[0]){
        REPORT_TYPE_MISSMATCH(*l->getType(), *rtype, @2);
      }
      $$ = new shared_ptr<Expression>(gemv(l, r));
    }
    // Vector-Matrix
    else if (ltype->getOrder() == 1 && rtype->getOrder() == 2) {
      if (ltype->getDimensions()[0] != rtype->getDimensions()[0]){
        REPORT_TYPE_MISSMATCH(*ltype, *rtype, @2);
      }
      $$ = new shared_ptr<Expression>(gevm(l,r));
    }
    // Matrix-Matrix
    else if (ltype->getOrder() == 2 && rtype->getOrder() == 2) {
      if (ltype->getDimensions()[1] != rtype->getDimensions()[0]){
        REPORT_TYPE_MISSMATCH(*ltype, *rtype, @2);
      }
      $$ = new shared_ptr<Expression>(gemm(l,r));
    }
    else {
      REPORT_ERROR("cannot multiply >2-order tensors using *", @2);
      $$ = NULL;
    }
  }
  | expr "/" expr {
    $$ = NULL;
    delete $1;
    delete $3;
  }
  | expr "\\" expr {
    $$ = NULL;
    delete $1;
    delete $3;
  }
  | expr "'" {
    if ($1 == NULL) {  // TODO: Remove this check
      $$ = NULL;
      break;
    }

    auto expr = shared_ptr<Expression>(*$1);
    delete $1;

    CHECK_IS_TENSOR(expr, @1);

    TensorType *type = tensorTypePtr(expr->getType());

    switch (type->getOrder()) {
      case 0:
        // OPT: This might lead to redundant code to be removed in later pass
        $$ = new shared_ptr<Expression>(unaryElwiseExpr(IndexExpr::NONE, expr));
        break;
      case 1:
        // OPT: This might lead to redundant code to be removed in later pass
        $$ = new shared_ptr<Expression>(unaryElwiseExpr(IndexExpr::NONE, expr));
        if (!ctx->isColumnVector(*expr->getType())) {
          ctx->toggleColumnVector(*(*$$)->getType());
        }
        break;
      case 2:
        $$ = new shared_ptr<Expression>(transposeMatrix(expr));
        break;
      default:
        REPORT_ERROR("cannot transpose >2-order tensors using '", @1);
        $$ = NULL;
    }
  }
  | expr "^" expr {
    $$ = NULL;
    delete $1;
    delete $3;
  }
  | expr "==" expr {
    $$ = NULL;
    delete $1;
    delete $3;
  }
  | expr "!=" expr {
    $$ = NULL;
    delete $1;
    delete $3;
  }
  | expr "<"  expr {
    $$ = NULL;
    delete $1;
    delete $3;
  }
  | expr ">"  expr {
    $$ = NULL;
    delete $1;
    delete $3;
  }
  | expr "<=" expr {
    $$ = NULL;
    delete $1;
    delete $3;
  }
  | expr ">=" expr {
    $$ = NULL;
    delete $1;
    delete $3;
  }
  | "(" expr ")" {
    $$ = NULL;
    delete $2;
  }
  | expr ":" expr {
    $$ = NULL;
    delete $1;
    delete $3;
  }
  | expr "." expr {
    $$ = NULL;
    delete $1;
    delete $3;
  }
  | call_expr {
    $$ = NULL;
  }
  | map_expr {
    $$ = NULL;
  }

%union {
  IndexExpr::Operator binop;
};
%destructor {} <binop>;

%type <binop> elwise_binary_op;
elwise_binary_op
  : "+" {
    $$ = IndexExpr::ADD;
  }
  | "-" {
    $$ = IndexExpr::SUB;
  }
  | ".*" {
    $$ = IndexExpr::MUL;
  }
  | "./" {
    $$ = IndexExpr::DIV;
  }

%union {
  std::shared_ptr<Call> *call;
};
%type<call> call_expr;
call_expr
  : IDENT "(" actual_list ")" {
    std::string name = convertAndFree($1);
    auto call = new Call(name, *$actual_list);
    delete $actual_list;
    $$ = new std::shared_ptr<Call>(call);
  }

%type <expressions> actual_list;
actual_list
  : /* empty */ {
    $$ = new vector<shared_ptr<Expression>>();
  }
  | expr_list {
    $$ = $expr_list;
  }

%type <expressions> expr_list;
expr_list
  : expr {
    $$ = new std::vector<std::shared_ptr<Expression>>();
    if ($expr == NULL) break;  // TODO: Remove check
    $$->push_back(*$expr);
    delete $expr;
  }
  | expr_list "," expr {
    $$ = $1;
    if ($expr == NULL) break;  // TODO: Remove check
    $$->push_back(*$expr);
    delete $expr;
  }

// Map expressions
map_expr
  : "map" IDENT "to" IDENT with reduce {
    string function($2);
    string target($4);
    free((void*)$2);
    free((void*)$4);
  }

with
  : /* empty */
  | "with" IDENT {
    std::string neighbor = convertAndFree($IDENT);
  }

reduce
  : /* empty */
  | "reduce" reduction_op

reduction_op
  : "+"
  | "*"


// Lhs expressions
%code requires {
  namespace {
  // This struct describe variable accesses.
  struct VariableAccess {
    std::string name;
  };

  typedef simit::util::OwnershipVector<VariableAccess*> VariableAccessVector;
  }
};

%union {
  VariableAccess       *VarAccess;
  VariableAccessVector *VarAccesses;
};

%type<VarAccesses> lhs_expr_list;
lhs_expr_list
  : lhs_expr {
    $$ = new VariableAccessVector();
    if ($lhs_expr == NULL) break;  // TODO: Remove check
    $$->push_back($lhs_expr);
  }
  | lhs_expr_list "," lhs_expr {
    $$ = $1;
    if ($lhs_expr == NULL) break;  // TODO: Remove check
    $$->push_back($lhs_expr);
  }

%type<VarAccess> lhs_expr;
lhs_expr
  : IDENT {
    std::string name = convertAndFree($IDENT);
    auto variableStore = new VariableAccess;
    variableStore->name = name;
    $$ = variableStore;
  }
  | IDENT "." IDENT {
    std::string name = convertAndFree($1);
    std::string field = convertAndFree($3);
    $$ = NULL;
  }
  | IDENT "(" expr_list ")" {
    std::string name = convertAndFree($IDENT);
    $$ = NULL;
    delete $expr_list;
  }


// Types
%union {
  Type                         *type;

  TensorType                   *tensorType;
  std::vector<IndexSetProduct> *IndexSetProducts;
  std::vector<IndexSet>        *IndexSets;
  IndexSet                     *indexSet;
  ComponentType                 componentType;
};
%destructor {} <componentType>;

%type <type>             type;

%type <tensorType>       tensor_type;
%type <IndexSetProducts> nested_dimensions;
%type <IndexSets>        dimensions;
%type <indexSet>         dimension;
%type <componentType>    component_type;

%type <elementType>      element_type;

type
  : set_type {
    $$ = NULL;
  }
  | element_type {
//    $$ = $element_type;
  }
  | tensor_type {
    $$ = $tensor_type;
  }

set_type
  : element_type "{" "}"
  | element_type "[" endpoints "]" "{" "}"

element_type
  : IDENT {
    std::string name = convertAndFree($IDENT);
    $$ = ctx->getElementType(name);
  }

tensor_type
  : component_type {
    $$ = new TensorType($component_type);
  }
  | TENSOR nested_dimensions "(" component_type ")" {
    $$ = new TensorType($component_type, *$nested_dimensions);
    delete $nested_dimensions;
  }
  | TENSOR nested_dimensions "(" component_type ")" "'" {
    $$ = new TensorType($component_type, *$nested_dimensions);
    ctx->toggleColumnVector(*$$);
    delete $nested_dimensions;
  }

nested_dimensions
  : /* empty */ {
    $$ = new std::vector<IndexSetProduct>();
  }
  | nested_dimensions "[" dimensions "]" {
    $$ = $1;

    auto parentDims = $$;
    auto childDims = unique_ptr<std::vector<IndexSet>>($dimensions);

    // If there are no previous dimensions then create IndexSetProducts
    if (parentDims->size() == 0) {
      for (auto &dim : *childDims) {
        UNUSED(dim);
        parentDims->push_back(IndexSetProduct());
      }
    }

    // Handle case where there are more child than parent dimensions
    if (childDims->size() > parentDims->size()) {
      for (size_t i=0; i < childDims->size() - parentDims->size(); ++i) {
        size_t numNestings = (*parentDims)[0].getFactors().size();
        std::vector<IndexSet> indexSets(numNestings, IndexSet(1));
        parentDims->push_back(IndexSetProduct(indexSets));
      }
    }

    // Handle case where there are more parent than child dimensions
    if (parentDims->size() > childDims->size()) {
      for (size_t i=0; i < parentDims->size() - childDims->size(); ++i) {
        childDims->push_back(IndexSet(1));
      }
    }

    // Multiply each dimension with the corresponding dimension in the shape.
    assert(childDims->size() == parentDims->size());
    for (size_t i=0; i<$$->size(); ++i) {
      (*parentDims)[i] = (*parentDims)[i] * (*childDims)[i];
    }
  }

dimensions
  : dimension {
    $$ = new std::vector<IndexSet>();
    $$->push_back(*$dimension);
    delete $dimension;
  }
  | dimensions "," dimension {
    $$ = $1;
    $$->push_back(*$dimension);
    delete $dimension;
  }

dimension
  : INT_LITERAL {
    $$ = new IndexSet($INT_LITERAL);
  }
  | IDENT {
    string ident = convertAndFree($IDENT);
    $$ = new IndexSet(123456789);  // TODO: This needs to be a set dimension
  }
  | "*" {
    $$ = new IndexSet();
  }

component_type
  : INT {
    $$ = ComponentType::INT;
  }
  | FLOAT {
    $$ = ComponentType::FLOAT;
  }


// Literals
%code requires {
  namespace {
    template <typename T>
    class TensorValues {
     public:
      TensorValues() : dimSizes(1) {};

      void addValue(const T &val) {
        values.push_back(val);
        dimSizes[dimSizes.size()-1]++;
      }
      void addDimension() { dimSizes.push_back(1); }

      bool dimensionsMatch(const TensorValues<T> &other, std::string *errors) {
        assert(errors != NULL);
        std::string mismatchError = "missmatched dimension sizes";
        if (dimSizes.size()-1 != other.dimSizes.size()) {
          *errors = mismatchError;
          return false;
        }

        for (unsigned int i=0; i<dimSizes.size()-1; ++i) {
          if (dimSizes[i] != other.dimSizes[i]) {
            *errors = mismatchError;
            return false;
          }
        }
        return true;
      }

      void merge(const TensorValues<T> &other) {
        values.insert(values.end(), other.values.begin(), other.values.end());
        dimSizes[dimSizes.size()-1]++;
      }

      std::vector<unsigned int> dimSizes;
      std::vector<T>            values;
    };
  }
};

%union {
  std::shared_ptr<Literal> *TensorLiteral;
  TensorValues<double>     *TensorDoubleValues;
  TensorValues<int>        *TensorIntValues;
};

%type <TensorLiteral> literal;
%type <TensorLiteral> tensor_literal;
%type <TensorLiteral> dense_tensor_literal;
%type <TensorLiteral> scalar_literal;

%type <TensorDoubleValues> float_dense_tensor_literal;
%type <TensorDoubleValues> float_dense_ndtensor_literal;
%type <TensorDoubleValues> float_dense_matrix_literal;
%type <TensorDoubleValues> float_dense_vector_literal;

%type <TensorIntValues> int_dense_tensor_literal;
%type <TensorIntValues> int_dense_ndtensor_literal;
%type <TensorIntValues> int_dense_matrix_literal;
%type <TensorIntValues> int_dense_vector_literal;

literal
  : tensor_literal

tensor_literal
  : dense_tensor_literal
  | dense_tensor_literal "'" {
    $$ = $1;
    ctx->toggleColumnVector(*(*$$)->getType());
  }
  | scalar_literal

dense_tensor_literal
  : "[" float_dense_tensor_literal "]" {
    auto values = unique_ptr<TensorValues<double>>($2);
    auto isps = std::vector<IndexSetProduct>(values->dimSizes.rbegin(),
                                             values->dimSizes.rend());
    auto type = new TensorType(ComponentType::FLOAT, isps);
    auto literal = new Literal(shared_ptr<TensorType>(type), // TODO: <Type>
                               values->values.data());
    $$ = new shared_ptr<Literal>(literal);
  }
  | "[" int_dense_tensor_literal "]" {
    auto values = unique_ptr<TensorValues<int>>($2);
    auto isps = std::vector<IndexSetProduct>(values->dimSizes.rbegin(),
                                             values->dimSizes.rend());
    auto type = new TensorType(ComponentType::INT, isps);
    auto literal = new Literal(shared_ptr<TensorType>(type), // TODO: <Type>
                               values->values.data());
    $$ = new shared_ptr<Literal>(literal);
  }

float_dense_tensor_literal
  : float_dense_matrix_literal {
    // If the matrix has only one column then we discard that dimension and
    // treat it as a vector.
    if ($1->dimSizes[$1->dimSizes.size()-1] == 1) {
      $1->dimSizes.pop_back();
    }
  }
  | float_dense_ndtensor_literal

float_dense_ndtensor_literal
  : "[" float_dense_matrix_literal "]" {
    $$ = $2;
    $$->addDimension();
  }
  | float_dense_ndtensor_literal "," "[" float_dense_matrix_literal "]" {
    auto  left = unique_ptr<TensorValues<double>>($1);
    auto right = unique_ptr<TensorValues<double>>($4);

    string errorStr;
    if(!left->dimensionsMatch(*right, &errorStr)) {
      REPORT_ERROR(errorStr, @2);
    }
    left->merge(*right);
    $$ = left.release();
  }

float_dense_matrix_literal
  : float_dense_vector_literal {
    $$ = $1;
    $$->addDimension();
  }
  | float_dense_matrix_literal ";" float_dense_vector_literal {
    auto  left = unique_ptr<TensorValues<double>>($1);
    auto right = unique_ptr<TensorValues<double>>($3);

    string errorStr;
    if(!left->dimensionsMatch(*right, &errorStr)) {
      REPORT_ERROR(errorStr, @2);
    }

    left->merge(*right);
    $$ = left.release();
  }

float_dense_vector_literal
  : FLOAT_LITERAL {
    $$ = new TensorValues<double>();
    $$->addValue($1);
  }
  | float_dense_vector_literal "," FLOAT_LITERAL {
    $$ = $1;
    $$->addValue($3);
  }

int_dense_tensor_literal
  : int_dense_matrix_literal {
    // If the matrix has only one column then we discard that dimension and
    // treat it as a vector.
    if ($1->dimSizes[$1->dimSizes.size()-1] == 1) {
      $1->dimSizes.pop_back();
    }
  }
  | int_dense_ndtensor_literal

int_dense_ndtensor_literal
  : "[" int_dense_matrix_literal "]" {
    $$ = $2;
    $$->addDimension();
  }
  | int_dense_ndtensor_literal "," "[" int_dense_matrix_literal "]" {
    auto  left = unique_ptr<TensorValues<int>>($1);
    auto right = unique_ptr<TensorValues<int>>($4);

    string errorStr;
    if(!left->dimensionsMatch(*right, &errorStr)) {
      REPORT_ERROR(errorStr, @2);
    }
    left->merge(*right);
    $$ = left.release();
  }

int_dense_matrix_literal
  : int_dense_vector_literal {
    $$ = $1;
    $$->addDimension();
  }
  | int_dense_matrix_literal ";" int_dense_vector_literal {
    auto  left = unique_ptr<TensorValues<int>>($1);
    auto right = unique_ptr<TensorValues<int>>($3);

    string errorStr;
    if(!left->dimensionsMatch(*right, &errorStr)) {
      REPORT_ERROR(errorStr, @2);
    }

    left->merge(*right);
    $$ = left.release();
  }

int_dense_vector_literal
  : INT_LITERAL {
    $$ = new TensorValues<int>();
    $$->addValue($1);
  }
  | int_dense_vector_literal "," INT_LITERAL {
    $$ = $1;
    $$->addValue($3);
  }

scalar_literal
  : INT_LITERAL {
    auto scalarType = new TensorType(ComponentType::INT);
    auto literal = new Literal(std::shared_ptr<TensorType>(scalarType), &$1);
    $$ = new shared_ptr<Literal>(literal);
  }
  | FLOAT_LITERAL {
    auto scalarType = new TensorType(ComponentType::FLOAT);
    auto literal = new Literal(std::shared_ptr<TensorType>(scalarType), &$1);
    $$ = new shared_ptr<Literal>(literal);
  }

// Tests
%union { Test *test; };
%type <test> test;
test
  : "%!" call_expr "==" tensor_literal ";" {
    auto call = shared_ptr<Call>(*$call_expr);
    auto expected = shared_ptr<Literal>(*$tensor_literal);
    delete $call_expr;
    delete $tensor_literal;

    std::vector<std::shared_ptr<Literal>> literalArgs;
    literalArgs.reserve(call->getArguments().size());
    for (auto &arg : call->getArguments()) {
      std::shared_ptr<Literal> litarg = dynamic_pointer_cast<Literal>(arg);
      if (!litarg) {
        REPORT_ERROR("function calls in tests must have literal arguments", @1);
      }
      literalArgs.push_back(litarg);
    }
    assert(literalArgs.size() == call->getArguments().size());

    std::vector<std::shared_ptr<Literal>> expecteds;
    expecteds.push_back(expected);
    $$ = new Test(call->getName(), literalArgs, expecteds);
  }

%%
